archetype (adl_version=1.4; uid=2a27e473-e734-460c-8b9f-a0043a1003ac)
	openEHR-EHR-OBSERVATION.menstrual_diary.v0

concept
	[at0000]	-- Menstrual diary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim, John Tore Valand">
				["organisation"] = <"Helse Vest IKT">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Marit Alice Venheim">
		["organisation"] = <"Helse Vest IKT AS">
		["email"] = <"marit.alice.venheim@helse-vest-ikt-no">
		["date"] = <"2021-08-03">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om menstruasjonsblødning ved en spesifikk dag.">
			use = <"Brukes for å registrere detaljer om menstruasjonsblødning ved en spesifikk dag. For eksempel i en menstruasjonskalender eller dagbok.">
			keywords = <"menstruasjon", "mensen", "syklus", "dysmenoré", "smerte", "oppblåsthet">
			misuse = <"Brukes ikke for å registrere dato for siste normal menstruasjon, bruk arketypen EVALUATION.last_normal_menstrual_period for dette formålet.

Brukes ikke for å registrere generell informasjon om menstruasjon eller relaterte symptomer i en syklus, bruk arketypen OBSERVATION.menstruation for dette formålet.

Brukes ikke for å registrere detaljer om menarke eller overgangsalder - bruk arketypen EVALUATION.menstruation_summary for dette formålet.

Brukes ikke for å registrere informasjon om typiske eller vanlige menstruasjonssykluser eller mønstre - bruk arketypen EVALUATION.menstruation_summary for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about menstrual bleeding and associated symptoms within a specified day.">
			use = <"Use to record details about menstrual bleeding and associated symptoms within a specified day. For example in a period tracker or diary.">
			keywords = <"menstruation", "menses", "cycle", "menstrual", "dysmenorrhoea", "pain", "bloating">
			misuse = <"Not to be used to record the Last normal menstrual period (LNMP) use the EVALUATION.last_normal_menstrual_period archetype for this purpose.

Not to be used to record overall information about menses or related symptoms in a cycle, use the OBSERVATION.menstruation archetype for this purpose. 

Not to be used to record details about menarche or menopause, use the EVALUATION.menstruation_summary archetype for this purpose.

Not to be used to record information about an individual's menstruation history events or patterns of menstruation over time, use the EVALUATION.menstruation_summary archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"843A6E9BD71DF1037DCAAAB6B9D3545D">
		["build_uid"] = <"98b4068e-13b2-4482-b163-841aef121a5f">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Menstrual diary
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Flow
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0005,    -- Flooding
													at0006,    -- Heavy flow
													at0007,    -- Medium flow
													at0008,    -- Light flow
													at0009,    -- Spotting
													at0022]    -- No bleeding
												}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {    -- Volume
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
													>
												>
											>
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {    -- Blood clots
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0015,    -- Present
													at0016]    -- Absent
												}
											}
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {    -- Color
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0018] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0020] occurrences matches {0..*} matches {    -- Additional details
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.symptom_sign(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at0012] occurrences matches {0..*} matches {    -- Per day
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
						width matches {
							DV_DURATION matches {
								value matches {PT24H}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0011] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Menstrual diary">
					description = <"Details about menstrual bleeding and associated symptoms within a specified day.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Flow">
					description = <"Semi-quantitative estimation of menstrual volume per day.">
				>
				["at0005"] = <
					text = <"Flooding">
					description = <"For example a presence of heavy menstrual flow that exceeded capacity of menstrual products to absorb the flow.">
				>
				["at0006"] = <
					text = <"Heavy flow">
					description = <"For example needing to change a high-absorbency tampon or pad every one to four hours.">
				>
				["at0007"] = <
					text = <"Medium flow">
					description = <"For example needing to change a regular-absorbency tampon or pad every three to four hours.">
				>
				["at0008"] = <
					text = <"Light flow">
					description = <"For example needing to change a low- or regular-absorbency tampon or pad one to three times per day.">
				>
				["at0009"] = <
					text = <"Spotting">
					description = <"For example a drop or two of blood, not even requiring menstrual products.">
				>
				["at0010"] = <
					text = <"Volume">
					description = <"Estimated volume of menstrual blood loss per day.">
				>
				["at0011"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"Per day">
					description = <"To record a spesific single day in a cycle.">
				>
				["at0013"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR.">
				>
				["at0014"] = <
					text = <"Blood clots">
					description = <"Presence of blood clots.">
				>
				["at0015"] = <
					text = <"Present">
					description = <"Blood clots occurred during the menstrual flow.">
				>
				["at0016"] = <
					text = <"Absent">
					description = <"Blood clots did not occur during the menstrual flow.">
				>
				["at0017"] = <
					text = <"Color">
					description = <"Color of blood.">
				>
				["at0018"] = <
					text = <"Description">
					description = <"Narrative description of the specific day of the menstrual cycle.">
				>
				["at0020"] = <
					text = <"Additional details">
					description = <"Additional details about the specific day of the menstrual cycle.">
					comment = <"For example menstrual symptoms or pictorial blood loss assessment charts.">
				>
				["at0021"] = <
					text = <"Comment">
					description = <"Additional narrative about the specific day of the menstrual cycle, not captured in other fields.">
				>
				["at0022"] = <
					text = <"No bleeding">
					description = <"No menstrual bleeding.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Menstruasjonsdagbok">
					description = <"Detaljer om menstruasjonsblødningen og assosierte symptomer ved en spesifikk dag.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Blødningsmengde">
					description = <"Semikvantitativ estimering av blødningsmengde per dag.">
				>
				["at0005"] = <
					text = <"Lekkasje">
					description = <"For eksempel kraftig menstruasjonsblødning som overskred absorpsjonkapasiteten til sanitærbind og andre menstruasjonsprodukter.">
				>
				["at0006"] = <
					text = <"Kraftig menstruasjonsblødning">
					description = <"Må for eksempel bytte tampong eller bind, med høy absorpsjonsevne, hver time til hver fjerde time.">
				>
				["at0007"] = <
					text = <"Moderat menstruasjonsblødning">
					description = <"Må for eksempel bytte tampong eller bind, med vanlig absorpsjonsevne, hver tredje til fjerde time.">
				>
				["at0008"] = <
					text = <"Lett menstruasjonsblødning">
					description = <"Må for eksempel bytte tampong eller bind, med lav eller vanlig absorpsjonsevne, en til tre ganger per dag.">
				>
				["at0009"] = <
					text = <"Spotting">
					description = <"For eksempel en bloddråpe eller to, behøver ikke bruke menstruasjonsprodukter.">
				>
				["at0010"] = <
					text = <"Volum">
					description = <"Estimert blodtap per dag.">
				>
				["at0011"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"Per dag">
					description = <"For å registrere én bestemt dag i en syklus.">
				>
				["at0013"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0014"] = <
					text = <"Blodkoagler">
					description = <"Tilstedeværelse av koagler.">
				>
				["at0015"] = <
					text = <"Tilstede">
					description = <"Det forekom blodkoagler i menstruasjonsblødningen.">
				>
				["at0016"] = <
					text = <"Fraværende">
					description = <"Det forekom ikke blodkoagler i menstruasjonsblødningen.">
				>
				["at0017"] = <
					text = <"Farge">
					description = <"Fargen på blodet.">
				>
				["at0018"] = <
					text = <"Beskrivelse av spesifikk dag">
					description = <"Fritekstbeskrivelse av den spesifikke dagen i menstruasjonssyklusen.">
				>
				["at0020"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere strukturerte detaljer om den spesifikke dagen i menstruasjonssyklusen.">
					comment = <"For eksempel symptomer eller piktogram for vurdering av blodtap.">
				>
				["at0021"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om den spesifikke dagen i menstruasjonssyklusen som ikke er omfattet av andre felt.">
				>
				["at0022"] = <
					text = <"Ingen blødning">
					description = <"Ingen menstruasjonsblødning.">
				>
			>
		>
	>
