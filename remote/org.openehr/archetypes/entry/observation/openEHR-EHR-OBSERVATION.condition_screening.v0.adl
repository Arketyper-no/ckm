archetype (adl_version=1.4; uid=6f76003e-cd96-4e85-b1f5-6ee8677c2c12)
	openEHR-EHR-OBSERVATION.condition_screening.v0

concept
	[at0000]	-- Condition screening questionnaire
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim, Silje Ljosland Bakke">
				["organisation"] = <"Helse Vest IKT, Helse Vest IKT AS">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no, silje.ljosland.bakke@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2020-03-13">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung der Antworten in einem Personen- oder Selbstbeurteilungs-Screening-Fragebogen zur Ermittlung von Zuständen, einschließlich Schwangerschaft.">
			use = <"Verwenden Sie diesen Archetyp, um die Antworten in einem Personen- oder Selbstbeurteilungs-Screening-Fragebogen zur Ermittlung von Zuständen, einschließlich Schwangerschaft, darzustellen.

Wenn zusätzliche Informationen zu einem bestimmten Zustand oder einer bestimmten Diagnose dargestellt werden müssen, definieren Sie mit der LINK-Klasse im Referenzmodel eine Beziehung zwischen diesem Archetyp und einer Instanz des Archetyps EVALUATION.problem_diagnosis im derselben Template.

Wenn zusätzliche Informationen zu einer Schwangerschaft dargestellt werden müssen, verwenden Sie die LINK-Klasse im Referenzmodel, um eine Beziehung zwischen diesem Archetyp und den relevanten Archetypen zur Schwangerschaft im derselben Template darzustellen.

Häufige Anwendungsfälle sind unter anderem:
- Systematische Befragung bei jeder Beratung; oder
- Spezifische Befragung im Zusammenhang mit der Überwachung von Infektionskrankheiten.">
			misuse = <"Nicht zu verwenden, um Details zu einem bestimmten gesundheitlichen Zustand im Rahmen einer typischen klinischen Beratung darzustellen. Verwenden Sie dazu EVALUATION.problem_diagnosis.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere responsen i et individuelt eller selvrapportert registreringsskjema, som screener for tilstander, inkludert graviditet.">
			use = <"Brukes for å registrere responsen i et individuelt- eller selvrapportert registreringsskjema, som screener for tilstander, inkludert graviditet.

Vanlige bruksområder inkluderer, men er ikke begrenset til:
- Systematisk spørring i enhver konsultasjon; eller
- Spesifikk spørring relatert til overvåkning av infeksjonssykdom.">
			misuse = <"Skal ikke brukes for å registrere spesifikke detaljer om en tilstand som en del av en typisk klinisk konsultasjon. Bruk EVALUATION.problem_diagnosis for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the responses to an individual- or self-reported questionnaire screening for conditions, including pregnancy.">
			use = <"Use to record the responses to an individual- or self-reported questionnaire screening for conditions, including pregnancy.

If additional details about a specific condition or diagnosis need to be recorded, use the RM LINK class to define a relationship between this archetype and an instance of the EVALUATION.problem_diagnosis archetype within the same template.

If additional details about a pregnancy need to be recorded, use the RM LINK class to define a relationship between this archetype and relevant pregnancy archetype/s within the same template.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation; or
- Specific questioning related to infectious disease surveillance.">
			misuse = <"Not to be used to record details about a specific condition as part of a typical clinical consultation. Use EVALUATION.problem_diagnosis for this purpose.">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, UK", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"C0BE87875CA1B85571DCB206F404EC00">
		["build_uid"] = <"be3b9941-a4e4-484d-a12b-d47ed3dd0815">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact http://www.snomed.org/snomed-ct/get-snomedct or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Condition screening questionnaire
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0034] occurrences matches {0..1} matches {    -- Screening purpose
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0028] occurrences matches {0..1} matches {    -- Presence of any conditions?
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031,    -- Present
													at0032,    -- Absent
													at0033]    -- Unknown
												}
											}
										}
									}
									CLUSTER[at0022] occurrences matches {0..*} matches {    -- Specific condition
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0004] matches {    -- Condition name
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0005] occurrences matches {0..1} matches {    -- Presence?
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0023,    -- Present
															at0024,    -- Absent
															at0027]    -- Unknown
														}
													}
												}
											}
											ELEMENT[at0006] occurrences matches {0..1} matches {    -- Onset
												value matches {
													DV_DATE_TIME matches {*}
												}
											}
											allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {    -- Condition details
												include
													archetype_id/value matches {/openEHR-EHR-CLUSTER\.clinical_evidence\.v1/}
											}
											ELEMENT[at0035] occurrences matches {0..1} matches {    -- Comment
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0029] occurrences matches {0..1} matches {    -- Pregnancy gestation
										value matches {
											DV_DURATION matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0036,    -- First trimester
													at0037,    -- Second trimester
													at0038]    -- Third trimester
												}
											}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {    -- Overall comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Condition screening questionnaire">
					description = <"An individual- or self-reported questionnaire screening for conditions, including problems, diagnoses and pregnancy.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Condition name">
					description = <"Name of the condition being screened.">
				>
				["at0005"] = <
					text = <"Presence?">
					description = <"Presence of the condition.">
				>
				["at0006"] = <
					text = <"Onset">
					description = <"The date/time when the condition was first noticed by the individual or diagnosed by a clinician.">
					comment = <"Partial dates are allowed.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0022"] = <
					text = <"Specific condition">
					description = <"Grouping of data elements related to screening for a single condition.">
				>
				["at0023"] = <
					text = <"Present">
					description = <"The specific condition is present.">
				>
				["at0024"] = <
					text = <"Absent">
					description = <"The specific condition is not present.">
				>
				["at0025"] = <
					text = <"Overall comment">
					description = <"Additional narrative about the conditions, not captured in other fields.">
				>
				["at0027"] = <
					text = <"Unknown">
					description = <"It is not known whether the condition is present or absent.">
				>
				["at0028"] = <
					text = <"Presence of any conditions?">
					description = <"Presence of any relevant conditions.">
				>
				["at0029"] = <
					text = <"Pregnancy gestation">
					description = <"The date/time when any conditions were first noticed.">
					comment = <"Partial dates are allowed.">
				>
				["at0031"] = <
					text = <"Present">
					description = <"Conditions are present.">
				>
				["at0032"] = <
					text = <"Absent">
					description = <"Conditions are not present.">
				>
				["at0033"] = <
					text = <"Unknown">
					description = <"It is not known whether any conditions are present or absent.">
				>
				["at0034"] = <
					text = <"Screening purpose">
					description = <"The reason for overall screening.">
					comment = <"For example: pre-operative screening.">
				>
				["at0035"] = <
					text = <"Comment">
					description = <"Additional narrative about the specific condition, not captured in other fields.">
				>
				["at0036"] = <
					text = <"First trimester">
					description = <"Between first day of last period and the end of week 12.">
				>
				["at0037"] = <
					text = <"Second trimester">
					description = <"Between week 13 and week 28.">
				>
				["at0038"] = <
					text = <"Third trimester">
					description = <"After week 28.">
				>
				["at0039"] = <
					text = <"Condition details">
					description = <"Additional details about the specific condition.">
					comment = <"For example: Clinical evidence or a disease specific grading scale.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Spørreskjema for tilstander">
					description = <"Et individuelt eller selvrapportert spørreskjema for registrering av tilstedeværelse av tilstander, inkludert problemer, diagnoser og graviditet.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Navn på tilstand">
					description = <"Navnet på tilstanden som blir registrert.">
				>
				["at0005"] = <
					text = <"Tilstede?">
					description = <"Tilstedeværelse av tilstanden.">
				>
				["at0006"] = <
					text = <"Debut">
					description = <"Datoen/klokkeslettet da tilstanden først ble lagt merke til av den enkelte eller diagnostisert av en kliniker.">
					comment = <"Delvis dato er tillatt.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0022"] = <
					text = <"Spesifikk tilstand">
					description = <"Gruppering av dataelementer relatert til registreringen for en enkel tilstand.">
				>
				["at0023"] = <
					text = <"Tilstede">
					description = <"Den spesifikke tilstanden er tilstede.">
				>
				["at0024"] = <
					text = <"Fraværende">
					description = <"Den spesifikke tilstanden er fraværende.">
				>
				["at0025"] = <
					text = <"Overordnet kommentar">
					description = <"Ytterligere kommentar til tilstandene, som ikke er fanget opp i andre felt.">
				>
				["at0027"] = <
					text = <"Ukjent">
					description = <"Det er ukjent om tilstanden er tilstede eller fraværende.">
				>
				["at0028"] = <
					text = <"Er det noen tilstander tilstede?">
					description = <"Tilstedeværelse av relevante tilstander.">
				>
				["at0029"] = <
					text = <"Tidspunkt i svangerskap">
					description = <"Dato/klokkeslett da tilstandene først ble lagt merke til.">
					comment = <"Delvis dato er tillatt.">
				>
				["at0031"] = <
					text = <"Tilstede">
					description = <"Tilstander er tilstede.">
				>
				["at0032"] = <
					text = <"Fraværende">
					description = <"Tilstander er ikke tilstede.">
				>
				["at0033"] = <
					text = <"Ukjent">
					description = <"Det er ukjent om noen tilstander er tilstede eller fraværende.">
				>
				["at0034"] = <
					text = <"Registreringsformål">
					description = <"Årsaken til registreringen.">
					comment = <"For eksempel: Pre-operativ screening.">
				>
				["at0035"] = <
					text = <"Kommentar">
					description = <"Ytterligere kommentar til den spesifikke tilstanden som ikke er fanget opp i andre felt.">
				>
				["at0036"] = <
					text = <"Første trimester">
					description = <"Mellom første dag i siste periode og uke 12+6.">
				>
				["at0037"] = <
					text = <"Andre trimester">
					description = <"Mellom uke 13+0 og uke 28+6.">
				>
				["at0038"] = <
					text = <"Tredje trimester">
					description = <"Etter uke 28+6.">
				>
				["at0039"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om den spesifikke tilstanden.">
					comment = <"For eksempel klinisk grunnlag eller sykdomsspesifikke graderingsskalaer.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Screening-Fragebogen zur Ermittlung von Zuständen">
					description = <"Ein Personen- oder Selbstbeurteilungs-Screening-Fragebogen zur Ermittlung von Zuständen, einschließlich Problemen, Diagnosen und Schwangerschaft.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Beliebiges Ereignis">
					description = <"Standardwert, ein undefinierter/s Zeitpunkt oder Intervallereignis, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Name des Zustands">
					description = <"Name des geprüften Zustands.">
				>
				["at0005"] = <
					text = <"Vorhanden?">
					description = <"Vorhandensein des Zustands.">
				>
				["at0006"] = <
					text = <"Beginn">
					description = <"Das Datum / die Uhrzeit, wann der Zustand zum ersten Mal von der Person bemerkt oder von einem Arzt diagnostiziert wurde.">
					comment = <"Erlaubt sind Angaben als Teildatum.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Informationen zur Erfassung lokaler Inhalte oder Anpassung an andere Referenzmodelle/Formalismen.">
					comment = <"Zum Beispiel: Lokaler Informationsbedarf oder zusätzliche Metadaten zur Anpassung an FHIR-Ressourcen oder CIMI-Modelle.">
				>
				["at0022"] = <
					text = <"Bestimmter Zustand">
					description = <"Gruppierung von Datenelementen im Zusammenhang mit dem Screening auf einen einzelnen Zustand.">
				>
				["at0023"] = <
					text = <"Vorhanden">
					description = <"Der spezifische Zustand ist vorhanden.">
				>
				["at0024"] = <
					text = <"Nicht vorhanden">
					description = <"Der spezifische Zustand ist nicht vorhanden.">
				>
				["at0025"] = <
					text = <"Allgemeiner Kommentar">
					description = <"Zusätzliche Beschreibung über die Zustände, die nicht in anderen Bereichen dargestellt wurde.">
				>
				["at0027"] = <
					text = <"Unbekannt">
					description = <"Es ist nicht bekannt, ob der spezifische Zustand vorhanden oder nicht vorhanden ist.">
				>
				["at0028"] = <
					text = <"Vorhandensein von Zuständen?">
					description = <"Vorhandensein relevanter Zustände.">
				>
				["at0029"] = <
					text = <"Zeit der Schwangerschaft">
					description = <"Das Datum / die Uhrzeit, wann der Zustand zum ersten Mal festgestellt wurde.">
					comment = <"Erlaubt sind Angaben als Teildatum.">
				>
				["at0031"] = <
					text = <"Vorhanden">
					description = <"Zustände sind vorhanden.">
				>
				["at0032"] = <
					text = <"Nicht vorhanden">
					description = <"Keine Zustände sind vorhanden.">
				>
				["at0033"] = <
					text = <"Unbekannt">
					description = <"Es ist nicht bekannt, ob Zustände vorhanden oder nicht vorhanden sind.">
				>
				["at0034"] = <
					text = <"Zweck des Screenings">
					description = <"Der Grund für das gesamte Screening.">
					comment = <"Zum Beispiel: präoperatives Screening.">
				>
				["at0035"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Beschreibung des spezifischen Zustands, die nicht in anderen Bereichen dargestellt wurde.">
				>
				["at0036"] = <
					text = <"Erster Trimester">
					description = <"Zwischen dem ersten Tag der letzten Periode und dem Ende der 12. Woche.">
				>
				["at0037"] = <
					text = <"Zweiter Trimester">
					description = <"Zwischen 13. und 28. Woche.">
				>
				["at0038"] = <
					text = <"Dtritter Trimester">
					description = <"Nach der 28. Woche.">
				>
				["at0039"] = <
					text = <"Detaillierte Angaben zum Zustand">
					description = <"Zusätzliche Details zum spezifischen Zustand.">
					comment = <"Zum Beispiel: Klinische Evidenz oder eine krankheitsspezifische Bewertungsskala.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0023"] = <[SNOMED-CT::410515003]>
				["at0024"] = <[SNOMED-CT::410516002]>
				["at0031"] = <[SNOMED-CT::410515003]>
				["at0032"] = <[SNOMED-CT::410516002]>
				["at0027"] = <[SNOMED-CT::261665006]>
				["at0033"] = <[SNOMED-CT::261665006]>
			>
		>
	>
