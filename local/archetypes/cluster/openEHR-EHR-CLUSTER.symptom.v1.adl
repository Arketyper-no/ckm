archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.symptom.v1

concept
	[at0000]	-- Symptom
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Jasmin Buck, Sebastian Garde">
				["organisation"] = <"University of Heidelberg, Central Queensland University">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Lars Bitsch-Larsen; John Tore Valand">
				["organisation"] = <"Haukeland University Hospital of Bergen, Norway">
				["email"] = <"lbla@helse-bergen.no">
			>
			accreditation = <"MD, DEAA, MBA, spec in anesthesia, spec in tropical medicine.">
		>
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			author = <
				["name"] = <"Mona Saleh">
			>
		>
	>
description
	original_author = <
		["name"] = <"Tony Shannon">
		["organisation"] = <"UK NHS, Connecting for Health">
		["email"] = <"tony.shannon@nhs.net">
		["date"] = <"2007-02-20">
	>
	details = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			purpose = <"*To record detail about a symptom - either self-recorded by an individual or recorded on the behalf of a patient by a clinician. A complete patient history may include varying level of details about a variety of symptoms.(en)">
			use = <"*Use to record detailed information about a symptom as told to a clinician by a patient or self-recorded by the individual/patient.

This archetype allows a 'nil significant' statement to be explicitly recorded.(en)">
			misuse = <"*Not to be used to record details about pain. Use the specialisation of this archetype - the CLUSTER.symptom-pain instead.

Not to be used for diagnoses and problems that form part of a persisting Problem List - use EVALUATION.problem_diagnosis.(en)">
			copyright = <"© Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record detail about a single symptom as described by an individual - either self-recorded or documented on their behalf by a clinician as part of a clinical history record. 

A complete patient history may include varying level of details about a multiple symptoms.">
			use = <"Use to record detailed information about a single symptom.

This archetype has been designed as a generic framework for all symptoms. 

It is intended to be inserted into the 'Details' SLOT within the OBSERVATION.story archetype or in any other ENTRY or CLUSTER archetype requiring capture of symptom-related data. It can also be reused by inserting it within other CLUSTER.symptom archetypes - within either the context of the 'Associated symptoms' SLOT or 'Previous details' SLOT. The specific symptom will be identified by the value for the 'Symptom name' data element, preferably from a selected terminology where available.

Specific data elements that are uniquely and additionally required for the identified symptom can be included as a separate CLUSTER archetype inserted into the 'Specific Details' SLOT. For example, those required to record details about pain.

Some data elements will have values sets that are in common with all symptoms, for example Severity values as Mild, Moderate and Severe. Others, such as 'Impact' will require unique value sets that are relevant to the identified symptom, for example the functional impacts of hearing loss will be significantly different to pain.

The 'nil significant' data element reflects way to record a shorthand commonly used by clinicians who need to require that a symptom is not causing the patient any discomfort or disturbance. It is similar in nature to an exclusion statement but rather reflects that the physiological state of the patient is normal rather than excluding a specific clinical manifestation. Modelling of 'nil significant' as an explicit exclusion statement in the same way as 'No examination done' within examination CLUSTER archetypes has been deliberately avoided. As 'Nil Significant' is a commonly used attribute and usually recorded without any qualifier or reason, it is considered that using a separate CLUSTER or ELEMENT archetype in all relevant use cases will result in unjustifiable modelling overheads. It has been included as an explicit data element in the Symptom archetype for these pragmatic reasons. In contrast, only in relatively uncommon clinical situations will 'No Examination Done' need to be explictly recorded, yet the ability to record it unambiguously in situations such as examinations for medicolegal purposes will be important, usually alongside the reason why examination findings were not recorded.">
			keywords = <"complaint", "presenting", "symptom">
			misuse = <"Not to be used for recording objective observations of a physical or mental disturbance in the body of an individual - use OBSERVATION.exam and related examination CLUSTER archetypes for this purpose.

Not to be used for diagnoses and problems that form part of a persisting Problem List - use EVALUATION.problem_diagnosis.">
			copyright = <"© Nasjonal IKT HF">
		>
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"*To record detail about a symptom - either self-recorded by an individual or recorded on the behalf of a patient by a clinician. A complete patient history may include varying level of details about a variety of symptoms.(en)">
			use = <"*Use to record detailed information about a symptom as told to a clinician by a patient or self-recorded by the individual/patient.

This archetype allows a 'nil significant' statement to be explicitly recorded.(en)">
			misuse = <"*Sollte nur für Symptome benutzt werden. (en)">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For registrering av detaljer om et enkelt symptom som beskrevet av et individ. Symptomet kan enten registreres av individet selv eller av en kliniker som del av en anamnese.

En fullstendig anamnese kan inkludere varierende detaljering av flere symptomer.">
			use = <"Brukes til registrering av detaljert informasjon om et enkelt symptom.

Arketypen er designet for å gi et generisk rammeverk for alle symptomer.

Arketypene skal settes inn i \"Detaljer\"-SLOTet i OBSERVATION.story-arketypen eller i en hvilken som helst ENTRY eller CLUSTER-arketype som krever registrering av symptomrelaterte data. Arketypen kan også gjenbrukes i andre CLUSTER.symptom-arketyper i SLOTene \"Assosierte symptomer\" eller \"Tidligere detaljer\". Det spesifikke symptomet vil identifiseres ved verdien lagret i \"Symptomnavn\"-dataelementet, fortrinnsvis kodet med en terminologi.

Spesifikke dataelementer som er unike og kreves som tillegg for et spesifikt symptom, for eksempel detaljer rundt smerteregistrering, kan registreres i egne CLUSTER-arketyper lagt til i \"Spesifikke detaljer\" SLOTet.

Noen dataelementer vil ha verdisett som er felles for alle symptomer, for eksempel mild, moderat og alvorlig i feltet \"Alvorlighetsgrad\". Andre, som feltet \"Innvirkning\", krever egne verdisett som er relevante for det identifiserte symptomet, for eksempel hvordan funksjonelt hørselstap er markant annerledes enn smerte. 

Dataelementet \"Uten betydning\" gir en enkel metode for å eksplisitt registrere at et symptom ikke er tilstede eller ikke påfører pasienten ubehag eller uro. Det ligner et eksklusjonsutsagn, men gjenspeiler i stedet at pasientens fysiologiske tilstand er normal heller enn å ekskludere klinisk tilstedeværelse av symptomet.
Man har unngått å modellere \"Uten betydning\" som et eksplisitt eksklusjonsutsagt på samme måte som \"Undersøkelse ikke utført\" er unngått i CLUSTER-arketypene for kliniske undersøkelser. Siden \"Uten betydning\" er en vanlig brukt attributt, og vanligvis registreres uten at det registreres noen begrunnelse, er det vurdert slik at det vil føre til unødig merarbeid i videre modellering av templater dersom man benytter en egen CLUSTER-arketype for dette. Av disse pragmatiske årsakene er \"Uten betydning\" inkludert som et eget dataelement i symptomarketypen. I motsetning til dette vil det relativt sjelden være nødvendig å eksplisitt registrere at ingen undersøkelse ble utført, selv om det av medisinsk-juridiske årsaker vil være viktig å ha muligheten til å registrere det eksplisitt i enkelte situasjoner, som regel sammen med en begrunnelse for hvorfor undersøkelsesfunn ikke er registrert.



">
			keywords = <"lidelse", "symptom", "tilstedeværende">
			misuse = <"Skal ikke brukes til registrering av observasjoner av fysisk eller mental forstyrrelse i kroppen til et individ. Bruk OBSERVATION.exam og relaterte CLUSTER.exam-arketyper for dette formålet.

Brukes ikke til registrering av problemer og diagnoser som en del av en persistent problemliste, til dette brukes EVALUATION.problem_diagnosis.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Silje Ljosland Bakke, Helse Bergen HF, Norway (Editor)", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Sam Heard, Ocean Informatics", "Nils Kolstrup, Norway", "Heather Leslie, Ocean Informatics, Australia", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "alberto maldonado, UPV, Spain", "Luis Marco Ruiz, NST, Spain (Editor)", "Ian McNicoll, Ocean Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Editor)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"C1B820F2D881BD9DCB65077BBD02C0E7">
	>

definition
	CLUSTER[at0000] matches {	-- Symptom
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] matches {	-- Symptom name
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0035] occurrences matches {0..1} matches {	-- Nil significant
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0151] occurrences matches {0..*} matches {	-- Anatomical location
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0147] occurrences matches {0..1} matches {	-- Complex anatomical location
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.relative_location(-[a-zA-Z0-9_]+)*\.v1/}
				exclude
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0152] occurrences matches {0..1} matches {	-- Date/time of onset
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0028] occurrences matches {0..1} matches {	-- Duration
				value matches {
					DV_DURATION matches {
						value matches {PYMWDTHM}
					}
				}
			}
			ELEMENT[at0021] occurrences matches {0..1} matches {	-- Severity
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0023, 	-- Mild
							at0024, 	-- Moderate
							at0025]	-- Severe
						}
					}
				}
			}
			ELEMENT[at0155] occurrences matches {0..1} matches {	-- Impact
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0153] occurrences matches {0..*} matches {	-- Specific details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.pain(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Variation
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0004, 	-- Constant
							at0005, 	-- Intermittent
							at0006]	-- Fluctuating
						}
					}
				}
			}
			ELEMENT[at0037] occurrences matches {0..1} matches {	-- Course description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0161] occurrences matches {0..1} matches {	-- Date/time of resolution
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0031] occurrences matches {0..1} matches {	-- Number of previous episodes
				value matches {
					DV_COUNT matches {
						magnitude matches {|>=0|}
					}
				}
			}
			ELEMENT[at0057] occurrences matches {0..1} matches {	-- Description of previous episodes
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0090] occurrences matches {0..*} matches {	-- Previous Episode
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0142] occurrences matches {0..1} matches {	-- Date/time of onset
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0160] occurrences matches {0..1} matches {	-- Date/time of resolution
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0146] occurrences matches {0..*} matches {	-- Previous detail
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.symptom\.v1/}
					}
				}
			}
			CLUSTER[at0018] occurrences matches {0..*} matches {	-- Modifying Factor
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0019] occurrences matches {0..1} matches {	-- Factor
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {	-- Effect
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0156, 	-- No effect
									at0157, 	-- Precipitates
									at0158, 	-- Worsens
									at0159]	-- Relieves
								}
							}
						}
					}
					ELEMENT[at0056] occurrences matches {0..1} matches {	-- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0063] occurrences matches {0..*} matches {	-- Associated symptoms
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.symptom(-[a-zA-Z0-9_]+)*\.v1/}
			}
			allow_archetype CLUSTER[at0154] occurrences matches {0..*} matches {	-- Associated events
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.health_event(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}



ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["ar-sy"] = <
			items = <
				["at0000"] = <
					text = <"*Symptom(en)">
					description = <"*A subjective observation of a physical or mental disturbance in the body of an individual, as described by that individual. It can be either self-recorded, or recorded on the behalf of a patient by a clinician.(en)">
				>
				["at0001"] = <
					text = <"*Symptom name(en)">
					description = <"*The symptom experienced by the subject of care.(en)">
					comment = <"*Symptom name should be coded with a terminology, where possible.(en)">
				>
				["at0002"] = <
					text = <"*Description(en)">
					description = <"*Narrative description of the symptom as perceived by the subject of care.(en)">
				>
				["at0003"] = <
					text = <"*Variation(en)">
					description = <"*The variation of the symptom during its course since onset.(en)">
				>
				["at0004"] = <
					text = <"ثابت">
					description = <"العرض موجود بشكل ثابت/مستمر">
				>
				["at0005"] = <
					text = <"متقطع">
					description = <"العرض موجود في بعض الأوقات و غائب في البعض الآخر">
				>
				["at0006"] = <
					text = <"متموج">
					description = <"العرض يتغير بشكل مؤثر و لكنه موجود في جميع الأوقات">
				>
				["at0017"] = <
					text = <"*Effect(en)">
					description = <"*Perceived outcome after exposure of the variable on the symptom.(en)">
				>
				["at0018"] = <
					text = <"*Modifying Factor(en)">
					description = <"*Detail about how a specific factor impacts the symptom.(en)">
				>
				["at0019"] = <
					text = <"*Factor(en)">
					description = <"*Name of a variable whose impact is assessed on the symptom.(en)">
					comment = <"*Variables can include events, (en)">
				>
				["at0021"] = <
					text = <"*Severity(en)">
					description = <"*The overall intensity of the symptom as described by the subject of care.(en)">
				>
				["at0023"] = <
					text = <"متوسط">
					description = <"العرض لا يتداخل بشكل كبير مع الأنشطة اليومية">
				>
				["at0024"] = <
					text = <"متوسط">
					description = <"العرض يؤثر على الأنشطة اليومية">
				>
				["at0025"] = <
					text = <"شديد">
					description = <"العرض يمنع بعض الأنشطة اليومية المحورية">
				>
				["at0028"] = <
					text = <"المدة">
					description = <"مدة حدوث العرض منذ بدايته">
				>
				["at0031"] = <
					text = <"*Number of previous episodes(en)">
					description = <"*The number of times this symptom has previously occurred.(en)">
				>
				["at0035"] = <
					text = <"*Nil significant(en)">
					description = <"*The subject of care has not experienced any significant manifestation of the symptom.(en)">
					comment = <"*Record as True if the subject of care has not experienced the symptom.(en)">
				>
				["at0037"] = <
					text = <"*Course description(en)">
					description = <"*Narrative description about the course of the symptom since onset.(en)">
				>
				["at0056"] = <
					text = <"*Description(en)">
					description = <"*Narrative description of the effect of the variable on the symptom.(en)">
				>
				["at0057"] = <
					text = <"*Description of previous episodes(en)">
					description = <"*Narrative description of any or all previous episodes.(en)">
					comment = <"*  May include a comparison to this episode.(en)">
				>
				["at0063"] = <
					text = <"*Associated symptoms(en)">
					description = <"*Structured details about any associated symptoms.(en)">
				>
				["at0090"] = <
					text = <"النوبة السابقة">
					description = <"تفاصيل حول نوبة بعينها">
				>
				["at0142"] = <
					text = <"*Date/time of onset(en)">
					description = <"*The timing of the commencement of a previous episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0146"] = <
					text = <"*Previous detail(en)">
					description = <"*Structured details of the symptom during a previous episode.(en)">
				>
				["at0147"] = <
					text = <"*Complex anatomical location(en)">
					description = <"*Slot to contain detailed and structured anatomical location details.(en)">
					comment = <"*If the anatomical location is included in the Symptom name via precoordinated codes, use of this SLOT becomes redundant.(en)">
				>
				["at0151"] = <
					text = <"*Anatomical location(en)">
					description = <"*Record of a simple site on the body where the symptom was located.(en)">
					comment = <"*Occurrences for this data element are unbounded to allow for clinical scenarios such as describing a rash in multiple locations but where all of the other attributes such as impact and duration are identical. If the requirements for recording the body site are determined at run-time by the application or require more complex modelling such as relative locations then use the CLUSTER.anatomical_location or CLUSTER.relative_location within the 'Complex body site' SLOT in this archetype. 
If the anatomical location is included in the Symptom name via precoordinated codes, this data element becomes redundant.(en)">
				>
				["at0152"] = <
					text = <"*Date/time of onset(en)">
					description = <"*The timing of the onset for this episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0153"] = <
					text = <"*Specific details(en)">
					description = <"*Specific data elements that are additionally required to record as unique attributes of the identified symptom.(en)">
					comment = <"*For example, description of pain can require additional data elements describing the radiation and visual analogue rating scales.(en)">
				>
				["at0154"] = <
					text = <"*Associated events(en)">
					description = <"*Structured details about any associated health events that have occurred.(en)">
				>
				["at0155"] = <
					text = <"*Impact(en)">
					description = <"*Description of the functional impact of this symptom.(en)">
					comment = <"*For example: for hearing loss the impact may include 'Difficulty Hearing in Quiet Environment'; 'Difficulty Hearing the TV or Radio'; 'Difficulty Hearing Group Conversation'; and 'Difficulty Hearing on Phone'.(en)">
				>
				["at0156"] = <
					text = <"*No effect(en)">
					description = <"*Presence of the factor has no impact on the symptom.(en)">
				>
				["at0157"] = <
					text = <"*Precipitates(en)">
					description = <"*Presence of the factor causes onset of the symptom.(en)">
				>
				["at0158"] = <
					text = <"*Worsens(en)">
					description = <"*Presence of the factor exaccerbates the severity or impact of the symptom.(en)">
				>
				["at0159"] = <
					text = <"*Relieves(en)">
					description = <"*Presence of the factor reduces the severity or impact of the symptom.(en)">
				>
				["at0160"] = <
					text = <"*Date/time of resolution(en)">
					description = <"*The timing of the cessation of a previous episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0161"] = <
					text = <"*Date/time of resolution(en)">
					description = <"*The timing of the cessation of this episode of the symptom.(en)">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Symptom">
					description = <"A subjective observation of a physical or mental disturbance in the body of an individual, as described by that individual. It can be either self-recorded, or recorded on the behalf of a patient by a clinician.">
				>
				["at0001"] = <
					text = <"Symptom name">
					description = <"The symptom experienced by the subject of care.">
					comment = <"Symptom name should be coded with a terminology, where possible.">
				>
				["at0002"] = <
					text = <"Description">
					description = <"Narrative description of the symptom as perceived by the subject of care.">
				>
				["at0003"] = <
					text = <"Variation">
					description = <"The variation of the symptom during its course since onset.">
				>
				["at0004"] = <
					text = <"Constant">
					description = <"The symptom is constantly present.">
				>
				["at0005"] = <
					text = <"Intermittent">
					description = <"The symptom is present at times and not at others.">
				>
				["at0006"] = <
					text = <"Fluctuating">
					description = <"The symptom varys considerably but is almost always present.">
				>
				["at0017"] = <
					text = <"Effect">
					description = <"Perceived outcome after exposure of the variable on the symptom.">
				>
				["at0018"] = <
					text = <"Modifying Factor">
					description = <"Detail about how a specific factor impacts the symptom.">
				>
				["at0019"] = <
					text = <"Factor">
					description = <"Name of a variable whose impact is assessed on the symptom.">
					comment = <"Variables can include events, ">
				>
				["at0021"] = <
					text = <"Severity">
					description = <"The overall intensity of the symptom as described by the subject of care.">
				>
				["at0023"] = <
					text = <"Mild">
					description = <"The symptom does not interfere greatly with day to day activities.">
				>
				["at0024"] = <
					text = <"Moderate">
					description = <"The symptom interferes with day to day activities.">
				>
				["at0025"] = <
					text = <"Severe">
					description = <"The symptom prohibits some key daily activities.">
				>
				["at0028"] = <
					text = <"Duration">
					description = <"The duration of the symptom since onset.">
				>
				["at0031"] = <
					text = <"Number of previous episodes">
					description = <"The number of times this symptom has previously occurred.">
				>
				["at0035"] = <
					text = <"Nil significant">
					description = <"The subject of care has not experienced any significant manifestation of the symptom.">
					comment = <"Record as True if the subject of care has not experienced the symptom.">
				>
				["at0037"] = <
					text = <"Course description">
					description = <"Narrative description about the course of the symptom since onset.">
				>
				["at0056"] = <
					text = <"Description">
					description = <"Narrative description of the effect of the variable on the symptom.">
				>
				["at0057"] = <
					text = <"Description of previous episodes">
					description = <"Narrative description of any or all previous episodes.">
					comment = <"  May include a comparison to this episode.">
				>
				["at0063"] = <
					text = <"Associated symptoms">
					description = <"Structured details about any associated symptoms.">
				>
				["at0090"] = <
					text = <"Previous Episode">
					description = <"Details about a specific episode.">
				>
				["at0142"] = <
					text = <"Date/time of onset">
					description = <"The timing of the commencement of a previous episode of the symptom.">
					comment = <"Partial dates are permitted. The exact time of onset can be recorded, if relevant.">
				>
				["at0146"] = <
					text = <"Previous detail">
					description = <"Structured details of the symptom during a previous episode.">
				>
				["at0147"] = <
					text = <"Complex anatomical location">
					description = <"Slot to contain detailed and structured anatomical location details.">
					comment = <"If the anatomical location is included in the Symptom name via precoordinated codes, use of this SLOT becomes redundant.">
				>
				["at0151"] = <
					text = <"Anatomical location">
					description = <"Record of a simple site on the body where the symptom was located.">
					comment = <"Occurrences for this data element are unbounded to allow for clinical scenarios such as describing a rash in multiple locations but where all of the other attributes such as impact and duration are identical. If the requirements for recording the body site are determined at run-time by the application or require more complex modelling such as relative locations then use the CLUSTER.anatomical_location or CLUSTER.relative_location within the 'Complex body site' SLOT in this archetype. 
If the anatomical location is included in the Symptom name via precoordinated codes, this data element becomes redundant.">
				>
				["at0152"] = <
					text = <"Date/time of onset">
					description = <"The timing of the onset for this episode of the symptom.">
					comment = <"Partial dates are permitted. The exact time of onset can be recorded, if relevant.">
				>
				["at0153"] = <
					text = <"Specific details">
					description = <"Specific data elements that are additionally required to record as unique attributes of the identified symptom.">
					comment = <"For example, description of pain can require additional data elements describing the radiation and visual analogue rating scales.">
				>
				["at0154"] = <
					text = <"Associated events">
					description = <"Structured details about any associated health events that have occurred.">
				>
				["at0155"] = <
					text = <"Impact">
					description = <"Description of the functional impact of this symptom.">
					comment = <"For example: for hearing loss the impact may include 'Difficulty Hearing in Quiet Environment'; 'Difficulty Hearing the TV or Radio'; 'Difficulty Hearing Group Conversation'; and 'Difficulty Hearing on Phone'.">
				>
				["at0156"] = <
					text = <"No effect">
					description = <"Presence of the factor has no impact on the symptom.">
				>
				["at0157"] = <
					text = <"Precipitates">
					description = <"Presence of the factor causes onset of the symptom.">
				>
				["at0158"] = <
					text = <"Worsens">
					description = <"Presence of the factor exaccerbates the severity or impact of the symptom.">
				>
				["at0159"] = <
					text = <"Relieves">
					description = <"Presence of the factor reduces the severity or impact of the symptom.">
				>
				["at0160"] = <
					text = <"Date/time of resolution">
					description = <"The timing of the cessation of a previous episode of the symptom.">
					comment = <"Partial dates are permitted. The exact time of onset can be recorded, if relevant.">
				>
				["at0161"] = <
					text = <"Date/time of resolution">
					description = <"The timing of the cessation of this episode of the symptom.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"*Symptom(en)">
					description = <"*A subjective observation of a physical or mental disturbance in the body of an individual, as described by that individual. It can be either self-recorded, or recorded on the behalf of a patient by a clinician.(en)">
				>
				["at0001"] = <
					text = <"*Symptom name(en)">
					description = <"*The symptom experienced by the subject of care.(en)">
					comment = <"*Symptom name should be coded with a terminology, where possible.(en)">
				>
				["at0002"] = <
					text = <"*Description(en)">
					description = <"*Narrative description of the symptom as perceived by the subject of care.(en)">
				>
				["at0003"] = <
					text = <"*Variation(en)">
					description = <"*The variation of the symptom during its course since onset.(en)">
				>
				["at0004"] = <
					text = <"Dauerhaft">
					description = <"das Symptom ist dauerhaft anwesend">
				>
				["at0005"] = <
					text = <"Zeitweise">
					description = <"das Symptom ist abwechselnd anwesend und nicht anwesend">
				>
				["at0006"] = <
					text = <"Variabel">
					description = <"das Symptom variiert deutlich, ist aber praktisch ständig anwesend">
				>
				["at0017"] = <
					text = <"*Effect(en)">
					description = <"*Perceived outcome after exposure of the variable on the symptom.(en)">
				>
				["at0018"] = <
					text = <"*Modifying Factor(en)">
					description = <"*Detail about how a specific factor impacts the symptom.(en)">
				>
				["at0019"] = <
					text = <"*Factor(en)">
					description = <"*Name of a variable whose impact is assessed on the symptom.(en)">
					comment = <"*Variables can include events, (en)">
				>
				["at0021"] = <
					text = <"*Severity(en)">
					description = <"*The overall intensity of the symptom as described by the subject of care.(en)">
				>
				["at0023"] = <
					text = <"gelinde">
					description = <"das Symptom beeinträchtigt alltägliche Handlungen nicht ernsthaft">
				>
				["at0024"] = <
					text = <"mäßig">
					description = <"das Symptom beeinträchtigt alltägliche Handlungen">
				>
				["at0025"] = <
					text = <"schwer">
					description = <"das Symptom verhindert wichtige alltägliche Handlungen">
				>
				["at0028"] = <
					text = <"Dauer">
					description = <"die Dauer des Symptoms seit dem Beginn">
				>
				["at0031"] = <
					text = <"*Number of previous episodes(en)">
					description = <"*The number of times this symptom has previously occurred.(en)">
				>
				["at0035"] = <
					text = <"*Nil significant(en)">
					description = <"*The subject of care has not experienced any significant manifestation of the symptom.(en)">
					comment = <"*Record as True if the subject of care has not experienced the symptom.(en)">
				>
				["at0037"] = <
					text = <"*Course description(en)">
					description = <"*Narrative description about the course of the symptom since onset.(en)">
				>
				["at0056"] = <
					text = <"*Description(en)">
					description = <"*Narrative description of the effect of the variable on the symptom.(en)">
				>
				["at0057"] = <
					text = <"*Description of previous episodes(en)">
					description = <"*Narrative description of any or all previous episodes.(en)">
					comment = <"*  May include a comparison to this episode.(en)">
				>
				["at0063"] = <
					text = <"*Associated symptoms(en)">
					description = <"*Structured details about any associated symptoms.(en)">
				>
				["at0090"] = <
					text = <"*Previous episode">
					description = <"*Details about a specific episode (en)">
				>
				["at0142"] = <
					text = <"*Date/time of onset(en)">
					description = <"*The timing of the commencement of a previous episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0146"] = <
					text = <"*Previous detail(en)">
					description = <"*Structured details of the symptom during a previous episode.(en)">
				>
				["at0147"] = <
					text = <"*Complex anatomical location(en)">
					description = <"*Slot to contain detailed and structured anatomical location details.(en)">
					comment = <"*If the anatomical location is included in the Symptom name via precoordinated codes, use of this SLOT becomes redundant.(en)">
				>
				["at0151"] = <
					text = <"*Anatomical location(en)">
					description = <"*Record of a simple site on the body where the symptom was located.(en)">
					comment = <"*Occurrences for this data element are unbounded to allow for clinical scenarios such as describing a rash in multiple locations but where all of the other attributes such as impact and duration are identical. If the requirements for recording the body site are determined at run-time by the application or require more complex modelling such as relative locations then use the CLUSTER.anatomical_location or CLUSTER.relative_location within the 'Complex body site' SLOT in this archetype. 
If the anatomical location is included in the Symptom name via precoordinated codes, this data element becomes redundant.(en)">
				>
				["at0152"] = <
					text = <"*Date/time of onset(en)">
					description = <"*The timing of the onset for this episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0153"] = <
					text = <"*Specific details(en)">
					description = <"*Specific data elements that are additionally required to record as unique attributes of the identified symptom.(en)">
					comment = <"*For example, description of pain can require additional data elements describing the radiation and visual analogue rating scales.(en)">
				>
				["at0154"] = <
					text = <"*Associated events(en)">
					description = <"*Structured details about any associated health events that have occurred.(en)">
				>
				["at0155"] = <
					text = <"*Impact(en)">
					description = <"*Description of the functional impact of this symptom.(en)">
					comment = <"*For example: for hearing loss the impact may include 'Difficulty Hearing in Quiet Environment'; 'Difficulty Hearing the TV or Radio'; 'Difficulty Hearing Group Conversation'; and 'Difficulty Hearing on Phone'.(en)">
				>
				["at0156"] = <
					text = <"*No effect(en)">
					description = <"*Presence of the factor has no impact on the symptom.(en)">
				>
				["at0157"] = <
					text = <"*Precipitates(en)">
					description = <"*Presence of the factor causes onset of the symptom.(en)">
				>
				["at0158"] = <
					text = <"*Worsens(en)">
					description = <"*Presence of the factor exaccerbates the severity or impact of the symptom.(en)">
				>
				["at0159"] = <
					text = <"*Relieves(en)">
					description = <"*Presence of the factor reduces the severity or impact of the symptom.(en)">
				>
				["at0160"] = <
					text = <"*Date/time of resolution(en)">
					description = <"*The timing of the cessation of a previous episode of the symptom.(en)">
					comment = <"*Partial dates are permitted. The exact time of onset can be recorded, if relevant.(en)">
				>
				["at0161"] = <
					text = <"*Date/time of resolution(en)">
					description = <"*The timing of the cessation of this episode of the symptom.(en)">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Symptom">
					description = <"En subjektiv observasjon av en fysisk eller mental forstyrrelse i kroppen til et individ, som beskrevet av individet selv. Dette kan være selvrapportert eller registrert av en kliniker, på vegne av en pasient.">
				>
				["at0001"] = <
					text = <"Symptomnavn">
					description = <"Navn på symptomet som oppleves av helsetjenestemottaker.">
					comment = <"Symptomnavn bør kodes med en terminologi om mulig.">
				>
				["at0002"] = <
					text = <"Beskrivelse">
					description = <"Narrativ beskrivelse av symptomet, slik det oppfattes av helsetjenestemottakeren.">
				>
				["at0003"] = <
					text = <"Variasjon">
					description = <"Symptomets variasjon siden debut.">
				>
				["at0004"] = <
					text = <"Konstant">
					description = <"Symptomet er konstant tilstede.">
				>
				["at0005"] = <
					text = <"Intermitterende">
					description = <"Symptomet er veksler mellom å være tilstede og fraværende.">
				>
				["at0006"] = <
					text = <"Fluktuerende">
					description = <"Symptomet varierer betydelig, men er tilstede nesten hele tiden.">
				>
				["at0017"] = <
					text = <"Effekt">
					description = <"Observert effekt på symptomet etter eksponering av variabel.">
				>
				["at0018"] = <
					text = <"Modifiserende faktorer">
					description = <"Detaljer om hvordan spesifikke faktorer påvirker symptomet.">
				>
				["at0019"] = <
					text = <"Faktor">
					description = <"Navn på en variabel som har innvirkning på symptomet.">
					comment = <"Variabler kan omfatte hendelser.">
				>
				["at0021"] = <
					text = <"Alvorlighetsgrad">
					description = <"Intensiteten av symptomet som beskrevet av helsetjenestemottakeren.">
				>
				["at0023"] = <
					text = <"Mild">
					description = <"Symptomene har ikke innvirkning på daglige aktiviteter i vesentlig grad.">
				>
				["at0024"] = <
					text = <"Moderat">
					description = <"Symptomene har innvirkning på daglige aktiviteter.">
				>
				["at0025"] = <
					text = <"Alvorlig">
					description = <"Symptomene hindrer visse viktige daglige aktiviteter.">
				>
				["at0028"] = <
					text = <"Varighet">
					description = <"Varigheten til symptom siden debut.">
				>
				["at0031"] = <
					text = <"Antall tidligere episoder">
					description = <"Antall ganger symptomet tidligere har funnet sted.">
				>
				["at0035"] = <
					text = <"Uten betydning">
					description = <"Helsetjenestemottakeren har ikke erfart noen vesentlig manifestasjon av symptomet.">
					comment = <"Registrer som Sann dersom helsetjenestemottakeren ikke har opplevd symptomet i vesentlig grad.">
				>
				["at0037"] = <
					text = <"Forløpsbeskrivelse">
					description = <"Narrativ beskrivelse av symptomets forløp siden debut.">
				>
				["at0056"] = <
					text = <"Beskrivelse">
					description = <"Narrativ beskrivelse av faktorens effekt på symptomet.">
				>
				["at0057"] = <
					text = <"Beskrivelse av tidligere episoder">
					description = <"Narrativ beskrivelse av tidligere eller av alle tidligere episoder.">
					comment = <"Kan inkludere en sammenligning med den aktuelle episoden.">
				>
				["at0063"] = <
					text = <"Tilknyttede symptomer">
					description = <"Strukturerte detaljer om ethvert tilknyttet symptom.">
				>
				["at0090"] = <
					text = <"Tidligere episode">
					description = <"Detaljer om en spesifikk episode.">
				>
				["at0142"] = <
					text = <"Dato/tid for debut">
					description = <"Tidspunktet for debut av en tidligere episode av symptomet">
					comment = <"Partielle datoer er tillatt. Nøyaktig tid for symptomets debut kan registreres om relevant.">
				>
				["at0146"] = <
					text = <"Tidligere detaljer">
					description = <"Strukturerte detaljer om symptomet under tidligere episode.">
				>
				["at0147"] = <
					text = <"Kompleks anatomisk lokalisering">
					description = <"SLOT for detaljerte og strukturerte detaljer om anatomisk lokalisering.">
					comment = <"Hvis den anatomiske lokaliseringen allerede er satt i elementet \"Symptomnavn\" via prekoordinerte koder, blir dette SLOTet overflødig.">
				>
				["at0151"] = <
					text = <"Anatomisk lokalisering">
					description = <"Registrering av et enkelt område på kroppen hvor symptomet var lokalisert.">
					comment = <"Forekomster for dette dataelementet er satt ubegrenset for å tillate kliniske scenarier som eksempelvis utslett på flere steder på kroppen, men hvor alle andre attributter som \"Innvirkning\" og \"Varighet\" er identiske.

Om kravet for registrering av kroppsplassering er bestemt i en applikasjon eller krever en mer kompleks modellering som for eksempel relativ lokalisering, bruk arketypen CLUSTER.anatomical_location eller CLUSTER.relative_location innen \"Complex body site\"-SLOTet i denne arketypen.

Er den anatomiske lokaliseringen inkludert i symptomnavnet via prekoordinerte koder er dette dataelementet overflødig.">
				>
				["at0152"] = <
					text = <"Dato/tid for debut">
					description = <"Debuttidspunkt for denne episoden av symptomet.">
					comment = <"Partielle datoer er tillatt. Nøyaktig tid for symptomets debut kan registreres, dersom relevant.">
				>
				["at0153"] = <
					text = <"Spesifikke detaljer">
					description = <"Spesifikke tilleggsdataelementer som er nødvendige for å registrere unike attributter til det identifiserte symptomet.">
					comment = <"For eksempel: Beskrivelse av smerte kan kreve ekstra dataelementer som beskriver utstråling og visuell analog skala.">
				>
				["at0154"] = <
					text = <"Tilknyttede hendelser">
					description = <"Strukturerte detaljer om enhver tilknyttet helserelatert hendelse som har funnet sted.">
				>
				["at0155"] = <
					text = <"Funksjonell innvirkning">
					description = <"Beskrivelse av symptomets innvirkning på funksjon.">
					comment = <"For eksempel: For hørselstap vil innvirkning kunne omfatte \"Vansker med å høre i et stille miljø\", \"Vansker med å høre TV eller radio\"; \"Vansker med å høre gruppesametaler\" og \"Vansker med å høre i telefon\".">
				>
				["at0156"] = <
					text = <"Ingen effekt">
					description = <"Tilstedeværelse av faktoren har ingen effekt på symptomet.">
				>
				["at0157"] = <
					text = <"Utløser">
					description = <"Tilstedeværelse av faktor fører til debut av symptomet.">
				>
				["at0158"] = <
					text = <"Forverrer">
					description = <"Tilstedeværelse av faktor forverrer alvorlighet eller funksjonell innvirkning av symptomet.">
				>
				["at0159"] = <
					text = <"Lindrer">
					description = <"Tilstedeværelse av faktor reduserer alvorligheten eller funksjonell innvirkning av symptomet.">
				>
				["at0160"] = <
					text = <"Dato/tid for opphør">
					description = <"Dato/tid for opphør av dette symptomet.">
					comment = <"Partielle datoer er tillatt. Nøyaktig tid for opphør av symptomet kan registreres om relevant.">
				>
				["at0161"] = <
					text = <"Dato/tid for opphør">
					description = <"Dato/tid for opphør av dette symptomet.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0000"] = <[SNOMED-CT::19019007]>
				["at0001"] = <[SNOMED-CT::19019007]>
				["at0002"] = <[SNOMED-CT::162408000]>
				["at0021"] = <[SNOMED-CT::162465004]>
				["at0023"] = <[SNOMED-CT::162468002]>
				["at0024"] = <[SNOMED-CT::162469005]>
				["at0025"] = <[SNOMED-CT::162470006]>
				["at0028"] = <[SNOMED-CT::162442009]>
			>
		>
	>
