archetype (adl_version=1.4; uid=a3f9bebc-ede3-45d2-a70b-74e176f0bbe5)
	openEHR-EHR-CLUSTER.occupation_record.v0

concept
	[at0000]	-- Occupation episode
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke">
				["organisation"] = <"Nasjonal IKT HF">
				["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2010-12-17">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere informasjon om en sysselsettingsepisode for et individ.">
			use = <"Brukes for å registrere informasjon om en sysselsettingsepisode for et individ. En aktiv eller pågående sysselsettingsepisode er antydet ved at man har registrert en dato for oppstart, men ikke en dato for opphør.

Arketypen er laget for å benyttes i SLOTet \"Sysselsetingsepisoder\" i arketypen CLUSTER.occupation_summary (Sysselsettingssammendrag), men kan også brukes innen EVALUATION.social_summary (Sosialanamnese) eller andre ENTRY- eller CLUSTER-arketyper der det er klinisk relevant.

Det kan være tilsynelatende eller reell overlapp mellom dataelementene i denne arketypen og opplysninger om sysselsetting/arbeidsforhold i demografisk informasjon i kliniske systemer. Dataelementene i denne arketypen er laget spesifikt for å støtte kliniske bruksområder som for eksempel legeerklæringer.">
			keywords = <"arbeid", "arbeidstaker", "arbeidsgiver", "arbeidsforhold", "arbeidshistorikk", "jobb", "ansatt", "yrke", "arbeidsløs", "studerer", "student", "elev", "trygdet", "ufør", "arbeidssituasjon", "erverv", "yrkestilknytning", "pensjon", "pensjonist", "attføring", "bransje", "arbeidsledig", "hjemmeværende", "stilling", "profesjon">
			misuse = <"Brukes ikke for å beskrive helserisikoer eller eksponering for farlige substanser i arbeidssituasjonen. Til dette brukes henholdsvis arketypene EVALUATION.health_risk eller EVALUATION.exposure.

Brukes ikke for å registrere informasjon om individets inntektskilder eller detaljer om inntekt.

Brukes ikke for å registrere informasjon om et individs sysselsetting på et vilkårlig tidspunkt (for eksempel 16. juni 2014) eller tidsintervall (for eksempel \"de siste 30 dager\" eller \"mellom 2014 og 2016\").">
			copyright = <"© 2010 NEHTA, openEHR Foundation, Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about an occupation episode of the individual.">
			use = <"Use to record information about an actual occupation episode of the individual. An active, or current episode of occupation may be implied from a 'Date commenced' but no 'Date ceased'. 

This archetype has been specifically designed to be used in the 'Occupation episodes' SLOT within the CLUSTER.occupation_summary archetype, but can also be used within EVALUATION.social_summary or any other ENTRY or CLUSTER archetypes, where clinically appropriate.

There may be some apparent or real overlap between the data elements in this archetype and occupation/employment details that may be stored as demographic details in clinical systems. The data elements have been designed specifically to support clinical purposes such as generation of medical certificates.">
			keywords = <"employment", "employer", "job", "occupation", "work", "profession", "unemployed", "employee", "unemployment", "studying", "employed", "student">
			misuse = <"Not to be used for detailed descriptions of health risks or exposure to hazardous substances in the workplace. Use the archetypes EVALUATION.health_risk or EVALUATION.exposure for this purpose.

Not to be used to record information about sources of income or income details for the individual.

Not to be used to record information about the historical occupation of an individual at an arbitrary point in time (such as on June 16, 2014) or during a specified interval of time (such as 'in the past 30 days' or 'between 2014-2016)'.">
			copyright = <"© 2010 NEHTA, openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Morten Aas, Oslo Universitetssykehus, Norway", "Tomas Alme, DIPS ASA, Norway", "Anne Pauline Anderssen, Helse Nord RHF, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Marcus Baw, openGPSoC / BawMedical Ltd, United Kingdom", "Ivar Berge, Oslo Universitetssykehus, Norway", "Bjørn Christensen, Helse Bergen HF, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Samuel Frade, Marand, Portugal", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Sergio Freire, State University of Rio de Janeiro, Brazil", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway (Nasjonal IKT redaktør)", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne Gunn Haugland, Helse Bergen HF, Norway", "Stig Erik Hegrestad, Helse Førde, Norway", "Kristian Heldal, Telemark Hospital Trust, Norway", "Anca Heyd, DIPS ASA, Norway", "Teresa Highway, Alberta Health Services, Canada", "Bjørg Eli Hollund, helse-bergen, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Susanna Jönsson, Landstinget i Värmland, Sweden", "Lars Morgan Karlsen, Nordlandssykehuset Bodø, Norway", "Tone Klund, DIPS AS, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Pedro Leuschner, Centro Hospitalar do Porto, Portugal", "Siv Marie Lien, DIPS ASA, Norway", "Hallvard Lærum, Direktoratet for e-helse, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "Erik Nissen, Cambio Healthcare Systems AB, Sweden", "Andrej Orel, Marand d.o.o., Slovenia", "Martin Paulson, Sykehuset i Vestfold, Norway", "Georg Reinhardt, Helse Fonna, Norway", "Tanja Riise, Nasjonal IKT HF, Norway", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Annette Hole Sjøborg, DIPS ASA, Norway", "Nyree Taylor, Ocean Informatics, Australia", "Jon Tysdahl, Furst medlab AS, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)", "Jørn Henrik Vold, Helse Bergen, Avdeling for rusmedisin, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Employment Summary, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2016-01-11]. No longer available.">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D7057E4BFACF07E809A7DD1091A8A2EF">
		["build_uid"] = <"b9608c07-efe8-41df-8fbb-903fcfa44aff">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Occupation episode
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Category
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Date commenced
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Organisation details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- Profession
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0005] occurrences matches {0..*} matches {	-- Title/role
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..1} matches {	-- Full time equivalent
				value matches {
					DV_PROPORTION matches {
						numerator matches {|>=0.0|}
						type matches {1, 2}
					}
				}
			}
			ELEMENT[at0019] occurrences matches {0..1} matches {	-- Hours
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::709]>
						list = <
							["1"] = <
								units = <"h/d">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["2"] = <
								units = <"h/wk">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["3"] = <
								units = <"h/mo">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["4"] = <
								units = <"h/a">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Status
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0018] occurrences matches {0..*} matches {	-- Additional details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0008] occurrences matches {0..1} matches {	-- Date ceased
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0014] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Occupation episode">
					description = <"Detailed information about the daily activity of an individual during a specified period of time.">
					comment = <"For example: employed, unemployed, carer, student and retired.">
				>
				["at0001"] = <
					text = <"Category">
					description = <"The category of the occupation episode.">
					comment = <"For example: employed, unemployed, student, retired, etc. Coding with a terminology is desirable, where possible. 'Unemployed' is strictly speaking not an occupation, but it's still relevant as an example for this element, as in some situations it may be necessary to document episodes of unemployment.">
				>
				["at0004"] = <
					text = <"Organisation details">
					description = <"Further details about the employer or educational institution.">
				>
				["at0005"] = <
					text = <"Title/role">
					description = <"Job title or role of the occupation episode.">
				>
				["at0006"] = <
					text = <"Profession">
					description = <"Classification of the type of profession undertaken by the individual.">
					comment = <"For example: categorisation according to the ISCO-08(http://www.ilo.org/global/publications/ilo-bookstore/order-online/books/WCMS_172572/lang--en/index.htm) major, sub-major, minor and unit groups..">
				>
				["at0007"] = <
					text = <"Date commenced">
					description = <"Date occupation episode commenced.">
				>
				["at0008"] = <
					text = <"Date ceased">
					description = <"Date occupation episode ceased.">
				>
				["at0013"] = <
					text = <"Full time equivalent">
					description = <"The position's fraction or percentage of a full time equivalent.">
				>
				["at0014"] = <
					text = <"Comment">
					description = <"Additional narrative about the occupation episode not captured in other fields.">
				>
				["at0015"] = <
					text = <"Status">
					description = <"The current status for the occupation episode.">
					comment = <"For example: on leave or sick leave, seconded, temporarily laid off. Coding with a terminology is desirable, where possible.">
				>
				["at0016"] = <
					text = <"Description">
					description = <"Narrative description of the occupation episode.">
				>
				["at0018"] = <
					text = <"Additional details">
					description = <"Further detail about the occupation episode.">
					comment = <"For example: income; workplace conditions or combat zone experience.">
				>
				["at0019"] = <
					text = <"Hours">
					description = <"The number of hours spent per time period at the specified occupation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Sysselsettingsepisode">
					description = <"Detaljert informasjon om et individs daglige aktivitet i et spesifikt tidsintervall.">
					comment = <"For eksempel: i arbeid, arbeidsløs, hjemmeværende, student, eller pensjonist.">
				>
				["at0001"] = <
					text = <"Kategori">
					description = <"Kategorien for sysselsettingsepisoden.">
					comment = <"For eksempel i arbeid, arbeidsløs, student, pensjonert, etc. Koding med en terminologi er ønskelig, der det er mulig. \"Arbeidsløs\" er strengt tatt ikke en kategori av sysselsetting, men er fremdeles relevant for dette elementet, da det i noen situasjoner er nødvendig å dokumentere tidsrom da individet har vært arbeidsløs.">
				>
				["at0004"] = <
					text = <"Organisasjonsdetaljer">
					description = <"Ytterligere detaljer om arbeidsgiver eller utdanningsinstitusjon.">
				>
				["at0005"] = <
					text = <"Tittel/rolle">
					description = <"Stillingstittel eller rolle for sysselsettingsepisoden.">
				>
				["at0006"] = <
					text = <"Profesjon">
					description = <"Klassifisering av typen profesjon individet har.">
					comment = <"For eksempel kategorisering i henhold til STYRK-08 (https://www.ssb.no/arbeid-og-lonn/artikler-og-publikasjoner/standard-for-yrkesklassifisering-styrk-08) \"yrkesfelt\", \"yrkesområder\", \"yrkesgrupper\" og \"yrker\".">
				>
				["at0007"] = <
					text = <"Dato for oppstart">
					description = <"Datoen sysselsettingsepisoden startet.">
				>
				["at0008"] = <
					text = <"Dato opphør">
					description = <"Datoen sysselsettingsepisoden opphørte.">
				>
				["at0013"] = <
					text = <"Stillingsandel">
					description = <"Stillingens andel av en fulltidsstilling.">
				>
				["at0014"] = <
					text = <"Kommentar">
					description = <"Ytterligere kommentar til sysselsettingsepisoden som ikke er fanget i andre felt.">
				>
				["at0015"] = <
					text = <"Status">
					description = <"Individets status for den aktuelle sysselsettingsepisoden.">
					comment = <"For eksempel sykemeldt, i aktiv sykemelding, i permisjon, utlånt eller permittert. Koding med en terminologi er ønskelig der det er mulig.">
				>
				["at0016"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av sysselsettingsepisoden.">
				>
				["at0018"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om sysselsettingsepisoden.">
					comment = <"For eksempel: Inntekt, forhold på arbeidsplassen eller erfaring fra krigsoner.">
				>
				["at0019"] = <
					text = <"Arbeidstimer">
					description = <"Antall timer per tidsenhet individet bruker på den aktuelle sysselsettingsepisoden.">
				>
			>
		>
	>
