archetype (adl_version=1.4; uid=fa03a98b-309c-4ba6-bd1a-6540a4dc3bdc)
	openEHR-EHR-CLUSTER.imaging_exam-hip_joint.v0
specialise
	openEHR-EHR-CLUSTER.imaging_exam.v1

concept
	[at0000.1]	-- Imaging examination of a hip joint
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen, John Tore Valand, Hanne Marte Bårholm">
				["organisation"] = <"Oslo University Hospital, Helse Vest IKT">
				["email"] = <"varntzen@ous-hf.no, john.tore.valand@helse-vest-ikt.no, hanne.marte.sandal.barholm@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Hanne Marte Bårholm">
		["organisation"] = <"Helse Vest IKT AS">
		["email"] = <"hanne.marte.sandal.barholm@helse-vest-ikt.no">
		["date"] = <"2023-03-15">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere funn ved radiologisk undersøkelse av ett hofteledd.">
			use = <"Brukes for å registrere funn ved radiologisk undersøkelse av ett hofteledd.

Det tiltenkte omfanget for denne arketypen er til slutt å kunne registrere alle detaljerte funn ved bildediagnostisk undersøkelse av hofteledd uavhengig av modalitet. Det er forventet at flere dataelementer kan bli lagt til når krav til slike er identifisert og bekreftet.

Denne arketypen er utformet for å bli nøstet i SLOT'et \"Ytterligere detaljer\" i SLOT'et \"Strukturerte funn\" i OBSERVATION.imaging_exam_result (Bildediagnostisk svar) eller i andre relevante Bildediagnostisk svar-arketyper, men kan også brukes innen andre ENTRY- og CLUSTER-arketyper der det er klinisk passende. Hvis denne arketypen, eller en spesialisering av denne, må utvides med flere nivåer med detaljerte funn eller observasjoner, kan flere forekomster av arketypen eller dens spesialiseringer nøstes i SLOT'et \"Ytterligere detaljer\".">
			keywords = <"funn", "observasjon", "bildediagnostikk", "kropp", "organ", "kroppsstruktur", "diagnostisk", "radiologi", "bilde", "hofte">
			misuse = <"Skal ikke brukes til å registrere funn som er gjort utenom det definerte organet eller den definerte kroppsdelen. Bruk en egen instans av en eller flere arketyper innen gruppen av CLUSTER.imaging_exam - arketyper (Undersøkelsesfunn) for dette formålet.

Skal ikke brukes til å registrere overordnede undersøkelsesfunn, kontekst eller tekniske detaljer relatert til bildeundersøkelsen. Bruk arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar) for å registrere for eksempel kommentar om undersøkelseskvalitet, differensialdiagnose og overordnet konklusjon.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the findings on radiological examination of a hip joint joint.">
			use = <"Use to record the findings on radiological examination of a hip joint.

The intended scope for this archetype is to eventually record all detailed findings on radiological examination of the hip joint using any modality. It is anticipated that further data elements may be added as requirements are identified and confirmed.

This archetype is designed to be nested within the 'Structured imaging findings' SLOT in the OBSERVATION.imaging_exam_result or within other relevant CLUSTER.imaging_exam archetypes, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate. If this archetype needs to be extended with further levels of structured findings, additional instances of the CLUSTER.imaging_exam or its specialisations can be nested in the 'Additional details' SLOT.">
			keywords = <"finding, imaging, body, system, structure, region, observation, diagnostic, radiology, image", ...>
			misuse = <"Not to be used to record findings observed outside the identified body structure or region. Use a separate instance of one or more archetypes from the CLUSTER.imaging_exam family for this purpose.

Not to be used to record details related to the overall findings, context or technical details related to a complete imaging examination test result. Use the OBSERVATION.imaging_exam_result for this purpose. For example, comments on the study quality, differential diagnoses, and overall impression.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Hanne Marte Bårholm, Helse Vest IKT, Norway (openEHR Editor)", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Nasjonal IKT">
		["references"] = <"Avgrenet fra: Imaging examination of a hip joint, Draft archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2023-05-22]. Available from: https://ckm.openehr.org/ckm/archetypes/1013.1.6841

Tönnis D, Brunken D. Eine Abgrenzung normaler und pathologischer Hüftpfannendachwinkel zur Diagnose der Hüftdysplasie. Auswertungen von 2294 Pfannendachwinkeln kindlicher Hüftgelenke [Differentiation of normal and pathological acetabular roof angle in the diagnosis of hip dysplasia. Evaluation of 2294 acetabular roof angles of hip joints in children]. Arch Orthop Unfallchir. 1968;64(3):197-228. German. doi: 10.1007/BF02171260. PMID: 5730180.">
		["original_namespace"] = <"no.nasjonalikt">
		["original_publisher"] = <"Nasjonal IKT">
		["custodian_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"CA58A7B79E943320FE3E0F6A6A0B0117">
		["build_uid"] = <"0d223c18-dcef-4f72-a23b-104678e58a03">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {    -- Imaging examination of a hip joint
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {    -- Body structure
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.1,    -- Left hip joint
							at0.2]    -- Right hip joint
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Imaging findings
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.imaging_exam-lymph_node(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node_group(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lesion(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {    -- Impression
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Acetabular roof angle
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::497]>
						list = <
							["1"] = <
								units = <"deg">
								magnitude = <|-360.0..360.0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.4] occurrences matches {0..1} matches {    -- Alpha angle (DDH)
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::497]>
						list = <
							["1"] = <
								units = <"deg">
								magnitude = <|-360.0..360.0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.6] occurrences matches {0..1} matches {    -- Alpha angle (cam morphology)
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::497]>
						list = <
							["1"] = <
								units = <"deg">
								magnitude = <|-360.0..360.0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.5] occurrences matches {0..1} matches {    -- Beta angle
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::497]>
						list = <
							["1"] = <
								units = <"deg">
								magnitude = <|-360.0..360.0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.7] occurrences matches {0..1} matches {    -- Joint space
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
							>
						>
					>
				}
			}
		}
	}

ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0.1"] = <
					text = <"Left hip joint">
					description = <"">
				>
				["at0.2"] = <
					text = <"Right hip joint">
					description = <"">
				>
				["at0.3"] = <
					text = <"Acetabular roof angle">
					description = <"The angle is formed by one line drawn from the medial edge of the acetabular sourcil and through to the most lateral aspect of the sourcil and a second line drawn in the horizontal plane of the pelvis. Also known as acetabular index.">
				>
				["at0.4"] = <
					text = <"Alpha angle (DDH)">
					description = <"The angle is formed by the acetabular roof to the vertical cortex of the ilium and thus reflects the depth of the bony acetabular roof.">
					comment = <"The alpha angle is a measurement used in the ultrasonographic assessment of developmental dysplasia of the hip (DDH), not to be confused with the alpha angle used in the evaluation of femoroacetabular impingement.">
				>
				["at0.5"] = <
					text = <"Beta angle">
					description = <"The angle is formed by the cartilaginous roof to the vertical cortex of the ilium and thus reflects the femoral head cartilaginous coverage.">
				>
				["at0.6"] = <
					text = <"Alpha angle (cam morphology)">
					description = <"The angle between a line from the center of the femoral head to the center of the femoral neck at its narrowest point, and a line from the center of the femoral head to a point where the distance from the bone to the center of the head is greater than the radius of the cartilage-covered femoral head.">
					comment = <"The alpha angle is a measurement for the detection of cam morphology, which is sometimes associated with femoroacetabular impingement (FAI). Not to be confused with the alpha angle used in the evaluation of developmental dysplasia of the hip (DDH).">
				>
				["at0.7"] = <
					text = <"Joint space">
					description = <"The space between the femoral head and the nearest part of the acetabular sourcil.">
				>
				["at0000.1"] = <
					text = <"Imaging examination of a hip joint">
					description = <"Findings on radiological examination of a hip joint.">
				>
				["at0001.1"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
					comment = <"For example: 'Liver', 'Right ankle' or 'Lymph node group'. Coding of the body structure with an appropriate terminology, such as SNOMED CT, is recommended. If the body structure has been fully identified in this 'Body structure' element, then the 'Body site' data element and 'Structured body site' SLOT become redundant.">
				>
				["at0000"] = <
					text = <"Imaging examination of a body structure">
					description = <"Findings on radiological examination of a specified body structure or region.">
				>
				["at0001"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
					comment = <"For example: 'Liver', 'Right ankle' or 'Lymph node group'. Coding of the body structure with an appropriate terminology, such as SNOMED CT, is recommended. If the body structure has been fully identified in this 'Body structure' element, then the 'Body site' data element and 'Structured body site' SLOT become redundant.">
				>
				["at0002"] = <
					text = <"Body site">
					description = <"Identification of the area of the body under examination.">
					comment = <"For example: a lymph node group (body structure) which found in the 'Right axilla'. Coding of 'Body site' with a terminology, such as SNOMED CT, is desirable. If the body site has been fully identified in the 'Body structure' data element, this data element becomes redundant.">
				>
				["at0003"] = <
					text = <"Structured body site">
					description = <"Structured details about the area of the body under examination.">
					comment = <"For example: details about a relative anatomical location, such as '2cm distal to the appendix'. If the body site has been fully identified in the 'Body structure' or 'Body site' data elements, then this SLOT becomes redundant.">
				>
				["at0004"] = <
					text = <"Imaging findings">
					description = <"Narrative description of the imaging findings observed during this examination.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about imaging findings for the identified body structure or region, or findings of related structures.">
				>
				["at0006"] = <
					text = <"Impression">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of all imaging findings for the identified body structure.">
					comment = <"Also referred to as 'Interpretation' or 'Opinion'. For example: 'No abnormality detected' or 'Free fluid present'. Coding of the 'Impression' with a terminology is permitted, if useful. Multiple occurrences allow for more than one coded impression. A narrative description may include a comparison with previous studies of the identified body structure, if appropriate.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the imaging findings of the body structure, not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0.1"] = <
					text = <"Venstre hofteledd">
					description = <"">
				>
				["at0.2"] = <
					text = <"Høyre hofteledd">
					description = <"">
				>
				["at0.3"] = <
					text = <"Acetabular helningsvinkel">
					description = <"Vinkelen formes av én linje som går mellom den mediale og den laterale acetabularkanten, og en annen linje som går gjennom bekkenets horisontale plan. Også kjent som acetabular index.">
				>
				["at0.4"] = <
					text = <"Alfavinkel (DDH)">
					description = <"Vinkelen dannes av acetabulartaket og den vertikale cortex av ilium og reflekterer dermed dybden av det benete acetabulartaket.">
					comment = <"Alfavinkelen er en måling gjort med ultralyd for å vurdere hofteleddsdysplasi. Må ikke forveksles med alfavinkelen brukt i vurdering av femoroacetabular impingement.">
				>
				["at0.5"] = <
					text = <"Beta vinkel">
					description = <"Vinkelen dannes av brusktaket til den vertikale cortex av ilium og reflekterer dermed femurhodets bruskdekning.">
				>
				["at0.6"] = <
					text = <"Alfavinkel (cam-morfologi)">
					description = <"Vinkelen dannes av en linje fra sentrum av femurhodet til sentrum av femurhalsens tynneste punkt, og en annen linje fra sentrum av femurhodet til et punkt hvor avstanden fra beinet til sentrum av hodet er større enn radiusen av det bruskdekkede femurhodet.">
					comment = <"Alfavinkelen er måling for å påvise cam-morfologi, som kan være assosiert med femoroacetabular impingement (FAI). Må ikke forveksles med alfavinkelen brukt for å vurdere hofteleddsdysplasi.">
				>
				["at0.7"] = <
					text = <"Leddspalte">
					description = <"Avstanden fra femurhodet til den nærmeste delen av acetabulum.">
				>
				["at0000.1"] = <
					text = <"Bildediagnostisk undersøkelse - hofteledd">
					description = <"Funn ved radiologisk undersøkelse av ett hofteledd.">
				>
				["at0001.1"] = <
					text = <"Kroppsstruktur">
					description = <"Identifisering av organet eller kroppsdelen som svaret gjelder.">
					comment = <"For eksempel \"Lever\", \"Høyre ankel\" eller \"Lymfeknutegruppe\". Det er anbefalt å kode kroppsstruktur med en passende terminologi, som SNOMED CT. Dersom den aktuelle strukturen i kroppen er fullt ut angitt i dette \"Kroppsstruktur\"-elementet er det ikke nødvendig å bruke \"Anatomisk lokalisasjon\" eller \"Strukturert anatomisk lokalisasjon\".">
				>
				["at0000"] = <
					text = <"Bildediagnostisk undersøkelse - kroppsstruktur">
					description = <"Funn eller observasjoner ved radiologisk undersøkelse av et definert organ eller en kroppsdel.">
				>
				["at0001"] = <
					text = <"Kroppsstruktur">
					description = <"Identifisering av organet eller kroppsdelen som svaret gjelder.">
					comment = <"For eksempel \"Lever\", \"Høyre ankel\" eller \"Lymfeknutegruppe\". Det er anbefalt å kode kroppsstruktur med en passende terminologi, som SNOMED CT. Dersom den aktuelle strukturen i kroppen er fullt ut angitt i dette \"Kroppsstruktur\"-elementet er det ikke nødvendig å bruke \"Anatomisk lokalisasjon\" eller \"Strukturert anatomisk lokalisasjon\".">
				>
				["at0002"] = <
					text = <"Anatomisk lokalisasjon">
					description = <"Identifisering av organ eller kroppsdel.">
					comment = <"For eksempel en lymfeknutegruppe (Kroppsstruktur) som er funnet i \"Høyre aksille\" (Anatomisk lokalisasjon). Det er ønskelig å kode \"Anatomisk lokalisasjon\" med en terminologi, som SNOMED CT. Dersom den anatomiske lokalisasjonen er fullt ut angitt i elementet \"Kroppsstruktur\" er det ikke nødvendig å bruke \"Anatomisk lokalisasjon\".">
				>
				["at0003"] = <
					text = <"Strukturert anatomisk lokalisasjon">
					description = <"Strukturerte detaljer om organ eller kroppsdel.">
					comment = <"For eksempel detaljert om en relativ anatomisk lokalisasjon, som \"2 cm distalt for appendix\". Dersom den nøyaktige eller relative anatomiske lokalisasjonen er fullt ut angitt i elementet \"Kroppsstruktur\" er det ikke nødvendig å bruke \"Strukturert anatomisk lokalisasjon\".">
				>
				["at0004"] = <
					text = <"Funn">
					description = <"Fritekstlig beskrivelse av funn eller observasjoner på bilder tatt under denne undersøkelsen.">
				>
				["at0005"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere strukturerte detaljer om funn eller observasjoner på bilder for det definerte organet eller kroppsdelen, eller om funn eller observasjoner hos tilhørende kroppsstrukturer.">
				>
				["at0006"] = <
					text = <"Resymé">
					description = <"Enkeltord, frase eller kortfattet beskrivelse som uttrykker den kliniske betydningen av alle funn og observasjoner for den definerte kroppsstrukturen.">
					comment = <"Kan også kalles \"Tolkning\" eller \"Inntrykk\". For eksempel \"Uten anmerkning\" eller \"Fri væske\". Dersom det er nyttig, er det åpent for å kode \"Resymé\" med en terminologi. Flere svar kan kodes ved at elementet er åpent for å være gjentakende. En beskrivelse i fritektst kan også brukes og kan inneholde en sammenligning med tidligere undersøkelser av samme kroppsstruktur.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om funn eller observasjoner på bilder av kroppsstrukturen og som ikke er fanget i andre felt.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::362905007]>
				["at0.2"] = <[SNOMED-CT::362906008]>
			>
		>
	>
