archetype (adl_version=1.4; uid=73a9b562-8616-495d-982d-bf6d34851850)
	openEHR-EHR-CLUSTER.communication.v0

concept
	[at0000]	-- Communication
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen HF, Norway">
				["email"] = <"john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-03-26">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere metoden(e) et individ bruker for å formidle eller motta informasjon.">
			use = <"Brukes for å registrere evne og metode for utveksling av informasjon med et individ.

Hensikten med arketypen er å registrere hvordan individet kommuniserer med andre og hvordan klinikere best kan kommunisere med individet.

Arketypen er laget for bruk i arketypen EVALUATION.social_summary (Sosialanamnese). Arketypen CLUSTER.language (Språk) kan nøstes i SLOTene \"Foretrukket kommunikasjon\" og \"Annen kommunikasjon\".">
			keywords = <"språk", "tolk", "oversettelse", "oversetter">
			misuse = <"Brukes ikke for å registrere detaljer om et språk, bruk arketypen CLUSTER.language (Språk) for dette formålet.

Brukes ikke for å registrere detaljer om en forespørsel om tolketjenester. Bruk arketypen CLUSTER.interpreter_requirements (Tolkebehov) i en egnet INSTRUCTION-arketype, for eksempel INSTRUCTION.service_request (Helsetjenesteforespørsel).

Brukes ikke for å registrere detaljer om tolking som er gjennomført. Bruk arketypen ACTION.interpretation for dette formålet.">
			copyright = <"© Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the means for exchanging of information with an individual.">
			use = <"Use to record the capability and means for exchanging of information with an individual.

This archetype is intent is to capture how the individual communicates with others and how clinicians can best communicate with the individual.

This archetype is designed to be used within the EVALUATION.social_summary archetype. The CLUSTER.language archetype is intended to be nested within the two SLOTS for 'Preferred communication' and 'Other communication'.">
			keywords = <"language", "interpreter", "translation", "translater", "translator">
			misuse = <"Not to be used to record details about a language - use CLUSTER.language for this purpose.

Not to be used to record details about a request for interpreter services - use CLUSTER.Interpreter_requirements with a suitable INSTRUCTION archetype, for example INSTRUCTION.service_request.

Not to be used to record details about an interpretation that was performed - use the proposed ACTION.interpretation.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Nasjonal IKT HF, Norway", "John Tore Valand, Helse Bergen HF, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Nasjonal IKT">
		["references"] = <"Avgrenet fra: Communication, Draft archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2018-08-07]. Available from: https://www.openehr.org/ckm/#showArchetype_1013.1.3155">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"no.nasjonalikt">
		["original_publisher"] = <"Nasjonal IKT">
		["custodian_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"D31B82518C72C40419E66696E33BFD97">
		["build_uid"] = <"a31a2ca5-8f87-4628-8ee6-5cf66d83007a">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Communication
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0002] occurrences matches {0..*} matches {	-- Translation requirements
				items cardinality matches {2..*; unordered} matches {
					ELEMENT[at0003] matches {	-- Language name
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] matches {	-- Translator required
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Preferred communication
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0006] occurrences matches {0..*} matches {	-- Other communication
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Communication">
					description = <"The means of exchanging of information with an individual.">
				>
				["at0001"] = <
					text = <"Description">
					description = <"Narrative description about the overall communication capability of an individual.">
				>
				["at0002"] = <
					text = <"Translation requirements">
					description = <"Details about whether translation is required for the language proposed for communication.">
					comment = <"In clinical systems it is common to record whether a interpreter is required as a simple, stand-alone boolean data point - the language for translation required is implied, rather than specified. However if this single boolean is exchanged to a location where the assumed language is different, the recorded boolean may be incorrect. In order for this data to be correctly exchanged, both the language and requirement for translation needs to be exchanged as a pair. For each language that is proposed for a clinical communication it will be necessary to do a separate assessment about whether a translator is required for the individual. For example, an English speaker in Quebec will not require an English interpreter but may require one if the consultation is being conducted in French. ">
				>
				["at0003"] = <
					text = <"Language name">
					description = <"Name of the language proposed for communication.">
				>
				["at0004"] = <
					text = <"Translator required">
					description = <"Indicator that the services of a translator are required for the identified language.">
				>
				["at0005"] = <
					text = <"Preferred communication">
					description = <"The preferred language and method of communication for an individual.">
					comment = <"More than one instances of the CLUSTER.language may be required here if the individual uses a variety modes of communication, for example speaking in one language and signing in another.">
				>
				["at0006"] = <
					text = <"Other communication">
					description = <"Other languages, and/or methods of communication for an individual.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the communication capability of an individual not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kommunikasjon">
					description = <"Metoder for informasjonsutveksling med et individ.">
				>
				["at0001"] = <
					text = <"Beskrivelse">
					description = <"En fritekstbeskrivelse om individets overordnede evne til kommunikasjon.">
				>
				["at0002"] = <
					text = <"Tolkebehov">
					description = <"Detaljer om tolking er nødvendig, eller ikke for språket som foreslås for kommunikasjonen.">
					comment = <"I kliniske system er det vanlig å registrere om det er behov for tolk med en registrering i et enkelt boolsk dataelement. Oversettelsesspråket er underforstått og spesifiseres ikke. Utveksles denne verdien til en lokalisering hvor oversettelsesspråket er et annet kan den registrerte boolske verdien være feil. For å sikre en riktig utveksling av dette dataelementet, må både språket og behov for tolk utveksles sammen. Hvert språk som individet snakker vil trenge en en egen vurdering av om det er et tolkebehov. For eksempel vil ikke en engelsktalende i Quebec trenge engelsk tolk, men det kan være behov for tolk om konsultasjonen utføres på fransk.">
				>
				["at0003"] = <
					text = <"Navn på språk">
					description = <"Navnet på det foreslåtte språket for kommunikasjonen.">
				>
				["at0004"] = <
					text = <"Trenger tolk">
					description = <"Indikererer at det er et tolkebehov på det spesifiserte språket.">
				>
				["at0005"] = <
					text = <"Foretrukket kommunikasjon">
					description = <"Individets foretrukne språk og kommunikasjonsmetode.">
					comment = <"Det kan være behov for flere instanser av CLUSTER.language (Språk) om individet benytter flere kommunikasjonsmetoder. For eksempel: snakker ett språk og benytter et annet for tegnspråk.">
				>
				["at0006"] = <
					text = <"Annen kommunikasjon">
					description = <"Individets andre språk og kommunikasjonsmetoder.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse om individets evne til kommunikasjon som ikke er registrert i andre felt.">
				>
			>
		>
	>
