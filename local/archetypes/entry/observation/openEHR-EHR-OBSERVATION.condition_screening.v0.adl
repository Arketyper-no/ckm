archetype (adl_version=1.4; uid=e6ca6269-4809-4dcf-9a8f-249602fd60cf)
	openEHR-EHR-OBSERVATION.condition_screening.v0

concept
	[at0000]	-- Problem/Diagnosis screening questionnaire
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim, Silje Ljosland Bakke, John Tore Valand">
				["organisation"] = <"Helse Vest IKT, Helse Vest IKT AS, Helse Bergen">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no, silje.ljosland.bakke@helse-vest-ikt.no, john.tore.valand@helse-bergen.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			author = <
				["name"] = <"Martijn van Eenennaam">
				["organisation"] = <"Nedap Healthcare">
				["email"] = <"martijn.vaneenennaam@nedap.com">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2020-03-13">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"*To create a framework for recording answers about problems or diagnoses, to the questions of a pre-defined screening questionnaire. (en)">
			use = <"*Use to create a framework for recording answers about problems or diagnoses, to the questions of a pre-defined screening questionnaire.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation, for example:
--- Diagnosed with cancer? Yes, No, Unkown.
--- Diagnosed with Covid 19 or Influenza in the past twelve months? Yes, No, Unkown.
- Specific questioning related to disease surveillance.

In use cases where it's important to differentiate the time context of a question in the persisted data, the reference model EVENT structure can be used to record this difference. For questions specifically about the point in time of recording, use a point in time EVENT, for example \"Do you have influenza?\". In contrast, for questions specifically about a period of time, use the interval event, for example, \"Have you had influenza in the past 4 weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening process identifies the presence of a health condition and it is intended that the details are to be recorded and persisted as part of an ongoing health record, any further specific details about the condition (such as the date of clinical recognition) should be recorded using the EVALUATION.problem_diagnosis archetype. (en)">
			keywords = <"*Condition, state, illness, syndrome (en)", ...>
			misuse = <"*Not to be used to record details about the positive presence of a specific condition as part of a typical clinical consultation. Use EVALUATION.problem_diagnosis for this purpose.

Not to be used to record details about issues, worries or concerns affecting an individual. Use OBSERVATION.issue_screening for this purpose.

Not to be used to record details about procedures performed. Use the OBSERVATION.procedure_screening for this purpose.

Not to be used to record details about the positive presence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.adverse_reaction_risk for this purpose.

Not to be used to record details about the positive absence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.exclusion_specific for this purpose. (en)">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å lage et rammeverk for registrering av svar på forhåndsdefinerte spørsmål om problemer eller diagnoser.">
			use = <"Brukes for å lage et rammeverk for registrering av svar på forhåndsdefinerte spørsmål om problemer eller diagnoser.

Vanlige bruksområder inkluderer, men er ikke begrenset til:
- Systematiske spørsmål ved konsultasjoner, for eksempel:
--- Diagnostisert med kreft? Ja, Nei, Ukjent.
--- Diagnostisert med Covid 19 eller influensa de siste tolv månedene? Ja, Nei, Ukjent.
- Spesifikke spørsmål relatert til overvåkning av sykdommer.

Semantikken til denne arketypen er med vilje løst definert, og spørringer etter data i denne arketypen vil normalt bare være nyttig eller trygt innenfor konteksten av hvert spesifikke spørreskjema. I et templat vil hvert dataelement vanligvis døpes om til det spesifikke spørsmålet. Der det er foreslått verdisett for vanlige brukstilfeller, kan disse tilpasses for lokal bruk ved å bruke 'tekst'-datatypen for å matche hvert enkelt brukstilfelle.

I tilfeller der det er viktig å skille tid i konteksten av spørsmål i de persisterte dataene, kan referansemodellens EVENT-struktur brukes for å registrere denne variasjonen. For spørsmål som spesifikt handler om tidspunktet ved registrering, bruk tidspunkt (point in time) EVENT. For eksempel: \"Har du influensa?\". Spørsmål som spesifikt omhandler en tidsperiode, bruk et intervall (interval) EVENT. For eksempel: \"Har du hatt influensa de siste 4 ukene?\".

Kilden til informasjonen i et kartleggingskjema kan variere i ulike kontekster, men kan identifiseres spesifikt ved å benytte \"Information provider\" elementet i openEHR referansemodellen.

Denne arketypen er laget for å bli brukt i et kartleggingsverktøy, for å registrere enkle strukturerte data til for eksempel et sykdomsregister. Dersom det i kartleggingsprosessen etableres at et problem eller en diagnose er tilstede, brukes arketypen EVALUATION.problem_diagnosis (Problem/diagnose) til å registrere tilhørende spesifikke detaljer som for eksempel diagnosedato, dersom detaljene bør være den del av den løpende journalen.">
			keywords = <"tilstand, sykdom, syndrom, spørreskjema", ...>
			misuse = <"Brukes ikke for å registrere detaljer om en etablert tilstedeværelse av et spesifikt problem eller diagnose pasientjournalen. BBruk arketypen EVALUATION.problem_diagnosis for dette formålet.

Brukes ikke for å registrere detaljer om etablert fravær av et spesifikt problem eller diagnose i pasientjournalen. Bruk arketypen EVALUATION.exclusion_specific for dette formålet.

Brukes ikke for å registrere detaljer om bekymringer eller forhold som gjelder et individ. Bruk arketypen OBSERVATION.issue_screening for dette formålet.

Brukes ikke for å registrere detaljer om gjennomførte prosedyrer, bruk arketypen ACTION.procedure for dette formålet.

Brukes ikke for å registrere detaljer om positiv tilstedeværelse av en overfølsomhetsreaksjon som en del av en klinisk konsultasjon. Bruk arketypen EVALUATION.adverse_reaction_risk for dette formålet.

Brukes ikke for å registrere detaljer om positivt fravær av en overfølsomhetsreaksjon som en del av en klinisk konsultasjon. Bruk arketypen EVALUATION.exclusion_specific for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To create a framework for recording answers to pre-defined screening questions about problems or diagnoses.">
			use = <"Use to create a framework for recording answers to pre-defined screening questions about problems or diagnoses.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation, for example:
--- Diagnosed with cancer? Yes, No, Unkown.
--- Diagnosed with Covid 19 or Influenza in the past twelve months? Yes, No, Unkown.
- Specific questioning related to disease surveillance.

The semantics of this archetype are intentionally loose, and querying this archetype would normally only be useful or safe within the context of each specific questionnaire. In a template, each data element would usually be renamed to the specific question asked. Where value sets have been proposed for common use cases, these can be adapted for local use by using the 'text' choice to match each specific use case.

In use cases where it's important to differentiate the time context of a question in the persisted data, the reference model EVENT structure can be used to record this difference. For questions specifically about the point in time of recording, use a point in time EVENT, for example \"Do you have influenza?\". In contrast, for questions specifically about a period of time, use the interval event, for example, \"Have you had influenza in the past 4 weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening process identifies the presence of a health condition and it is intended that the details are to be recorded and persisted as part of an ongoing health record, any further specific details about the condition (such as the date of clinical recognition) should be recorded using the EVALUATION.problem_diagnosis archetype.">
			keywords = <"Condition, state, illness, syndrome", ...>
			misuse = <"Not to be used to record details about the positive presence of a specific problem or diagnosis for persistence in a health record. Use EVALUATION.problem_diagnosis for this purpose.

Not to be used to record details about the positive absence of a specific problem or diagnosis for persistence in a health record. Use EVALUATION.exclusion_specific for this purpose.

Not to be used to record details about issues, worries or concerns affecting an individual. Use OBSERVATION.issue_screening for this purpose.

Not to be used to record details about procedures performed. Use the OBSERVATION.procedure_screening for this purpose.

Not to be used to record details about the positive presence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.adverse_reaction_risk for this purpose.

Not to be used to record details about the positive absence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.exclusion_specific for this purpose.">
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			purpose = <"*To create a framework for recording answers to pre-defined screening questions about problems or diagnoses.(en)">
			use = <"*Use to create a framework for recording answers to pre-defined screening questions about problems or diagnoses.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation, for example:
--- Diagnosed with cancer? Yes, No, Unkown.
--- Diagnosed with Covid 19 or Influenza in the past twelve months? Yes, No, Unkown.
- Specific questioning related to disease surveillance.

The semantics of this archetype are intentionally loose, and querying this archetype would normally only be useful or safe within the context of each specific questionnaire. In a template, each data element would usually be renamed to the specific question asked. Where value sets have been proposed for common use cases, these can be adapted for local use by using the 'text' choice to match each specific use case.

In use cases where it's important to differentiate the time context of a question in the persisted data, the reference model EVENT structure can be used to record this difference. For questions specifically about the point in time of recording, use a point in time EVENT, for example \"Do you have influenza?\". In contrast, for questions specifically about a period of time, use the interval event, for example, \"Have you had influenza in the past 4 weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening process identifies the presence of a health condition and it is intended that the details are to be recorded and persisted as part of an ongoing health record, any further specific details about the condition (such as the date of clinical recognition) should be recorded using the EVALUATION.problem_diagnosis archetype.(en)">
			keywords = <"Aandoening, toestand, ziekte, syndroom", ...>
			misuse = <"*Not to be used to record details about the positive presence of a specific problem or diagnosis for persistence in a health record. Use EVALUATION.problem_diagnosis for this purpose.

Not to be used to record details about the positive absence of a specific problem or diagnosis for persistence in a health record. Use EVALUATION.exclusion_specific for this purpose.

Not to be used to record details about issues, worries or concerns affecting an individual. Use OBSERVATION.issue_screening for this purpose.

Not to be used to record details about procedures performed. Use the OBSERVATION.procedure_screening for this purpose.

Not to be used to record details about the positive presence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.adverse_reaction_risk for this purpose.

Not to be used to record details about the positive absence of an adverse reaction risk as part of a typical clinical consultation. Use EVALUATION.exclusion_specific for this purpose.(en)">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (Nasjonal IKT redaktør)", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Yexuan Cheng, 浙江大学, China", "Are Edvardsen, SKDE, Helse Nord RHF, Norway", "Alexander Eikrem-Lüthi, Lovisenberg Diakonale Sykehus, Norway", "Kåre Flø, DIPS ASA, Norway", "Grant Forrest, Lunaria Ltd, United Kingdom", "Mikkel Johan Gaup Grønmo, Forvaltningssenter EPJ, Helse Nord RHF, Norway (Nasjonal IKT redaktør)", "Anca Heyd, DIPS ASA, Norway", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Liv Laugen, Oslo universitetssykehus, Norway (Nasjonal IKT redaktør)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, UK", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Andre Smitt-Ingebretsen, Sørlandet sykehus HF, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)", "Marit Alice Venheim, Helse Vest IKT, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"DAB0CD86DF9874B759E388366E49931F">
		["build_uid"] = <"8d6abe9e-2116-40af-98bb-066a5bbe9de1">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact http://www.snomed.org/snomed-ct/get-snomedct or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Problem/Diagnosis screening questionnaire
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0034] occurrences matches {0..1} matches {    -- Screening purpose
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0028] occurrences matches {0..1} matches {    -- Any problems or diagnoses?
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031,    -- Yes
													at0032,    -- No
													at0033]    -- Unknown
												}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0042] occurrences matches {0..*} matches {    -- Overall addtional details
										include
											archetype_id/value matches {/.*/}
									}
									CLUSTER[at0022] occurrences matches {0..*} matches {    -- Specific problem or diagnosis
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0004] matches {    -- Problem/Diagnosis name
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0005] occurrences matches {0..1} matches {    -- Presence?
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0023,    -- Yes
															at0024,    -- No
															at0027]    -- Unknown
														}
													}
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0040] occurrences matches {0..1} matches {    -- Age at diagnosis
												value matches {
													DV_TEXT matches {*}
													DV_INTERVAL<DV_DURATION> matches {*}
													DV_DURATION matches {*}
												}
											}
											ELEMENT[at0041] occurrences matches {0..1} matches {    -- Duration
												value matches {
													DV_DURATION matches {*}
													DV_INTERVAL<DV_DURATION> matches {*}
													DV_TEXT matches {*}
												}
											}
											allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {    -- Additional details
												include
													archetype_id/value matches {/openEHR-EHR-CLUSTER\.organisation\.v0/}
											}
											ELEMENT[at0025] occurrences matches {0..1} matches {    -- Comment
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Problem/Diagnosis screening questionnaire">
					description = <"Answers to pre-defined screening questions about problems or diagnoses.">
					comment = <"The answers may be self-reported.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Problem/Diagnosis name">
					description = <"Name of a problem or diagnosis; or grouping of problems or diagnoses.">
					comment = <"Coding of the Problem/Diagnosis name with a terminology is preferred, where possible.">
				>
				["at0005"] = <
					text = <"Presence?">
					description = <"Presence of the specific problem or diagnosis; or grouping of problems or diagnoses.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0022"] = <
					text = <"Specific problem or diagnosis">
					description = <"Details about a specific problem or diagnosis or grouping of problems or diagnoses related to the screening purpose.">
				>
				["at0023"] = <
					text = <"Yes">
					description = <"The specific problem or diagnosis; or grouping of problems or diagnoses is present.">
				>
				["at0024"] = <
					text = <"No">
					description = <"The specific problem or diagnosis; or grouping of problems or diagnoses is not present.">
				>
				["at0025"] = <
					text = <"Comment">
					description = <"Additional narrative about the specified problem or diagnosis, not captured in other fields.">
				>
				["at0027"] = <
					text = <"Unknown">
					description = <"It is not known whether the specific problem or diagnosis; or grouping of problems or diagnoses is present or absent.">
				>
				["at0028"] = <
					text = <"Any problems or diagnoses?">
					description = <"Presence of problems or diagnoses related to the screening purpose.">
				>
				["at0031"] = <
					text = <"Yes">
					description = <"Problems or diagnoses are present.">
				>
				["at0032"] = <
					text = <"No">
					description = <"Problems or diagnoses are not present.">
				>
				["at0033"] = <
					text = <"Unknown">
					description = <"It is not known whether any problems or diagnoses are present or absent.">
				>
				["at0034"] = <
					text = <"Screening purpose">
					description = <"The reason for overall screening.">
					comment = <"This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data.
For example: pre-admission screening or the name of the actual questionnaire.">
				>
				["at0039"] = <
					text = <"Additional details">
					description = <"Structured details or questions about the specific problem or diagnosis.">
					comment = <"For example; hospital where treated.">
				>
				["at0040"] = <
					text = <"Age at diagnosis">
					description = <"The age of the individual when the condition was diagnosed or recognised.">
				>
				["at0041"] = <
					text = <"Duration">
					description = <"Duration of time since diagnosis.">
				>
				["at0042"] = <
					text = <"Overall addtional details">
					description = <"Structured details or questions about the overall problems or diagnoses.">
				>
				["at0043"] = <
					text = <"Description">
					description = <"Narrative description about all problems or diagnosis.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kartlegging av problemer eller diagnoser">
					description = <"Svar på forhåndsdefinerte spørsmål om problemer eller diagnoser.">
					comment = <"Svarene kan være selvrapporterte.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Problem/diagnosenavn">
					description = <"Navnet på et problem eller en diagnose, eller grupper av problemer eller diagnoser.">
					comment = <"Koding av problem/diagnosenavnet med en terminologi foretrekkes, der det er mulig.">
				>
				["at0005"] = <
					text = <"Tilstede?">
					description = <"Tilstedeværelse av spesifisert problem/diagnose; eller gruppe av problemer eller diagnoser.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0022"] = <
					text = <"Spesifikt problem eller diagnose">
					description = <"Detaljer om et spesifikt problem eller diagnose eller grupper av problemer eller diagnoser relatert til kartleggingsformålet.">
				>
				["at0023"] = <
					text = <"Ja">
					description = <"Det spesifiserte problemet eller diagnosen; eller gruppen av problemer eller diagnoser er tilstede.">
				>
				["at0024"] = <
					text = <"Nei">
					description = <"Det spesifiserte problemet eller diagnosen; eller gruppen av problemer eller diagnoser er fraværende.">
				>
				["at0025"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om det spesifikke problemet eller diagnosen som ikke er omfattet av andre felt.">
				>
				["at0027"] = <
					text = <"Ukjent">
					description = <"Det er ukjent om det spesifiserte problemet eller diagnosen; eller gruppen av problemer eller diagnoser er tilstede eller fraværende.">
				>
				["at0028"] = <
					text = <"Tilstedeværelse av problemer eller diagnoser?">
					description = <"Tilstedeværelse av problemer eller diagnoser relatert til kartleggingsformålet.">
				>
				["at0031"] = <
					text = <"Ja">
					description = <"Problemer eller diagnoser er tilstede.">
				>
				["at0032"] = <
					text = <"Nei">
					description = <"Problemer eller diagnoser er ikke tilstede.">
				>
				["at0033"] = <
					text = <"Ukjent">
					description = <"Det er ukjent om noen problemer eller diagnoser er tilstede eller fraværende.">
				>
				["at0034"] = <
					text = <"Kartleggingsformål">
					description = <"Årsaken til kartleggingen.">
					comment = <"Dette dataelementet er ment for å sette en kontekst for spørsmålene om man senere ønsker å gjøre spørringer på dataene. Det er ikke forventet at dette dataelementet skal være synlig for en bruker av skjemaet, men kun tilgjengelig i de lagrede dataene. For eksempel: \"Pre operativ screening\" eller navnet på det faktiske spørreskjemaet.">
				>
				["at0039"] = <
					text = <"Ytterligere detaljer">
					description = <"Strukturerte detaljer eller spørsmål om det spesifikke problemet eller diagnosen.">
					comment = <"For eksempel: Behandlende sykehus.">
				>
				["at0040"] = <
					text = <"Alder ved diagnose">
					description = <"Individets alder da problemet/diagnosen ble diagnostisert eller oppdaget.">
				>
				["at0041"] = <
					text = <"Varighet">
					description = <"Tid/varighet siden diagnosen ble satt.">
				>
				["at0042"] = <
					text = <"Overordnede ytterligere detaljer">
					description = <"Strukturerte detaljer eller spørsmål om overordnede problemer eller diagnoser.">
				>
				["at0043"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om alle problemene eller diagnosene.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"*Problem/Diagnosis screening questionnaire (en)">
					description = <"*Answers to questions about problems or diagnoses in a pre-defined screening questionnaire. (en)">
					comment = <"*The answers may be self-reported. (en)">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Beliebiges Ereignis">
					description = <"Standardwert, ein undefinierter/s Zeitpunkt oder Intervallereignis, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Name des Zustands">
					description = <"*Name of a problem or diagnosis; or grouping of problems or diagnoses. (en)">
					comment = <"*Coding of the Problem/Diagnosis name with a terminology is preferred, where possible. (en)">
				>
				["at0005"] = <
					text = <"Vorhanden?">
					description = <"*Presence of the specific problem or diagnosis.  (en)">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Erweiterung">
					description = <"*Additional information required to extend the model with local content or to align with other reference models or formalisms. (en)">
					comment = <"*For example: local information requirements; or additional metadata to align with FHIR. (en)">
				>
				["at0022"] = <
					text = <"Bestimmter Zustand">
					description = <"*Details about a specific problem or diagnosis or grouping of problems or diagnoses related to the screening purpose. (en)">
				>
				["at0023"] = <
					text = <"*Yes (en)">
					description = <"Der spezifische Zustand ist vorhanden.">
				>
				["at0024"] = <
					text = <"*No (en)">
					description = <"Der spezifische Zustand ist nicht vorhanden.">
				>
				["at0025"] = <
					text = <"Kommentar">
					description = <"*Additional narrative about the specified condition, not captured in other fields. (en)">
				>
				["at0027"] = <
					text = <"Unbekannt">
					description = <"Es ist nicht bekannt, ob der spezifische Zustand vorhanden oder nicht vorhanden ist.">
				>
				["at0028"] = <
					text = <"*Any problems or diagnoses? (en)">
					description = <"*Presence of problems or diagnoses related to the screening purpose. (en)">
				>
				["at0031"] = <
					text = <"*Yes (en)">
					description = <"Zustände sind vorhanden.">
				>
				["at0032"] = <
					text = <"*No (en)">
					description = <"Keine Zustände sind vorhanden.">
				>
				["at0033"] = <
					text = <"Unbekannt">
					description = <"Es ist nicht bekannt, ob Zustände vorhanden oder nicht vorhanden sind.">
				>
				["at0034"] = <
					text = <"Zweck des Screenings">
					description = <"Der Grund für das gesamte Screening.">
					comment = <"*This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data.
For example: pre-admission screening or the name of the actual questionnaire. (en)">
				>
				["at0039"] = <
					text = <"*Additional details (en)">
					description = <"*Structured details or questions about the specific problem or diagnosis. (en)">
					comment = <"*For example; hospital where treated. (en)">
				>
				["at0040"] = <
					text = <"*Age at diagnosis (en)">
					description = <"*The age of the individual when the condition was diagnosed or recognised. (en)">
				>
				["at0041"] = <
					text = <"*Duration (en)">
					description = <"*Duration of time since diagnosis. (en)">
				>
				["at0042"] = <
					text = <"*Overall addtional details (en)">
					description = <"*Structured details or questions about the overall problems or diagnoses. (en)">
				>
				["at0043"] = <
					text = <"*Description (en)">
					description = <"*Narrative description about all problems or diagnosis. (en)">
				>
			>
		>
		["nl"] = <
			items = <
				["at0000"] = <
					text = <"Problem/Diagnosis screening questionnaire(en)">
					description = <"Answers to pre-defined screening questions about problems or diagnoses.(en)">
					comment = <"*The answers may be self-reported.(en)">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Gebeurtenis">
					description = <"Standaard, ongespecificeerd moment in tijd of een interval dat expliciet gedefiniëerd kan worden in een template of tijdens run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Problem/Diagnosis name(en)">
					description = <"Name of a problem or diagnosis; or grouping of problems or diagnoses.(en)">
					comment = <"*Coding of the Problem/Diagnosis name with a terminology is preferred, where possible.(en)">
				>
				["at0005"] = <
					text = <"*Aanwezig?">
					description = <"Presence of the specific problem or diagnosis; or grouping of problems or diagnoses.(en)">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Uitbreiding">
					description = <"Aanvullende informatie benodigd voor het vastleggen van lokale inhoud of om aan te sluiten bij referenctie modellen/formalismen.">
					comment = <"Bijvoorbeeld: lokale informatie eisen of aanvullende metadata om aan te sluiten bij FHIR of CIMI equivalenten.">
				>
				["at0022"] = <
					text = <"*Specifieke aandoening">
					description = <"Details about a specific problem or diagnosis or grouping of problems or diagnoses related to the screening purpose.">
				>
				["at0023"] = <
					text = <"*Ja">
					description = <"The specific problem or diagnosis; or grouping of problems or diagnoses is present.(en)">
				>
				["at0024"] = <
					text = <"*Nee">
					description = <"The specific problem or diagnosis; or grouping of problems or diagnoses is not present.(en)">
				>
				["at0025"] = <
					text = <"*Opmerking">
					description = <"Additional narrative about the specified problem or diagnosis, not captured in other fields.(en)">
				>
				["at0027"] = <
					text = <"*Onbekend">
					description = <"It is not known whether the specific problem or diagnosis; or grouping of problems or diagnoses is present or absent.(en)">
				>
				["at0028"] = <
					text = <"*Aanwezig?">
					description = <"Presence of problems or diagnoses related to the screening purpose.(en)">
				>
				["at0031"] = <
					text = <"*Ja">
					description = <"Problems or diagnoses are present.(en)">
				>
				["at0032"] = <
					text = <"*Nee">
					description = <"Problems or diagnoses are not present.(en)">
				>
				["at0033"] = <
					text = <"*Onbekend">
					description = <"It is not known whether any problems or diagnoses are present or absent.(en)">
				>
				["at0034"] = <
					text = <"*Doel van vragenlijst">
					description = <"De reden voor het uitvoeren van de screening met deze vragenlijst.">
					comment = <"This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data.
For example: pre-admission screening or the name of the actual questionnaire.(en)">
				>
				["at0039"] = <
					text = <"*Additional details(en)">
					description = <"Structured details or questions about the specific problem or diagnosis.(en)">
					comment = <"Bijvoorbeeld: ziekenhuis waar behandeling plaatsvond.">
				>
				["at0040"] = <
					text = <"*Leeftijd ten tijde van diagnose">
					description = <"The age of the individual when the condition was diagnosed or recognised.(en)">
				>
				["at0041"] = <
					text = <"*Duur">
					description = <"Tijdsduur sinds diagnose.">
				>
				["at0042"] = <
					text = <"*Overall addtional details(en)">
					description = <"*Structured details or questions about the overall problems or diagnoses.(en)">
				>
				["at0043"] = <
					text = <"*Description(en)">
					description = <"*Narrative description about all problems or diagnosis.(en)">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0023"] = <[SNOMED-CT::373066001]>
				["at0024"] = <[SNOMED-CT::373067005]>
				["at0031"] = <[SNOMED-CT::373066001]>
				["at0032"] = <[SNOMED-CT::373067005]>
				["at0027"] = <[SNOMED-CT::261665006]>
				["at0033"] = <[SNOMED-CT::261665006]>
			>
		>
	>
