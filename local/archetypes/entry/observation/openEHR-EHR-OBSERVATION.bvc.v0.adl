archetype (adl_version=1.4; uid=011ae22b-7185-4af4-ae52-d20f6887c2c6)
	openEHR-EHR-OBSERVATION.bvc.v0

concept
	[at0000]	-- Brøset Violence Checklist (BVC)
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim">
				["organisation"] = <"Helse Vest IKT">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"">
		["date"] = <"2020-11-13">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere resultatene for hvert parameter og deres totalsum for BVC skår.">
			use = <"Brukes for å registrere resultatene for hvert parameter og deres totalsum for BVC skår.

openEHR-arketyper er fritt tilgjengelige under en åpen lisens, men det spesifikke innholdet i denne Brøset Violence Checklist (BVC) er opphavsrettslig beskyttet. All bruk av denne arketypen i implementasjoner må gjøres i henhold til vilkår gitt av innehaverne av opphavsretten.
Erklæring om opphavsrett: © Copyright Linaker & Bush Iversen (1995). Almvik & Woods (2000). All rights reserved, do not use without the written permission of the copyright holders.
Informasjon om opphavsretten: http://www.riskassessment.no/#apps.">
			keywords = <"verbal, psykisk, angrep, voldelig, adferd", ...>
			misuse = <"">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter and their total sum for the BVC score.">
			use = <"Use to record the results for each component parameter and their total sum for the BVC score.

While openEHR archetypes are all freely available under an open license, the specific content of this Brøset Violence Checklist (BVC) archetype is copyright protected. Any use of this archetype within implementations must be in compliance with the terms established by the copyright owners.
Copyright statement: © Copyright Linaker & Bush Iversen (1995). Almvik & Woods (2000). All rights reserved, do not use without the written permission of the copyright holders.
Copyright information: http://www.riskassessment.no/#apps.">
			keywords = <"verbal, physical, atacking, violent, behaviour", ...>
			misuse = <"">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Hallvard Lærum", "Morten Hørthe, DIPS Norway", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Nasjonal IKT">
		["references"] = <"The Brøset Violence Checklist (BVC©) http://www.riskassessment.no/ , Retrived 20201201.

Hvidhjelm J et al. Sensitivity and specificity of the Brøset Violence Checklist as predictor of violence in forensic psychiatry. Nordic Journal of Psychiatry 2014 Volume 68, Issue 8

Almvik, R and Woods,P. Predicting inpatient violence using the Bröset Violence Checklist. International Journal of Psychiatric Nursing Research 1999 4 (3) 489-497

Woods P and Almvik R. The Brøset Violence Chekclist (BVC). Acta Psychiatrica Scandinavica 2002 106 (Suppl. 412) 103-105">
		["original_namespace"] = <"no.nasjonalikt">
		["original_publisher"] = <"Nasjonal IKT">
		["custodian_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"EDC374A62CEAC8224A0B34EB8763D4BC">
		["build_uid"] = <"f63f0921-ad30-494b-95fa-7249de3accd5">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Brøset Violence Checklist (BVC)
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {    -- Day shift
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Confused
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {    -- Irritable
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Boisterous
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {    -- Physical threats
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- Verbal threats
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Atacking objects
										value matches {
											0|[local::at0024],
											1|[local::at0018]
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {    -- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..6|}
											}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {    -- Interpretation
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031,    -- Small risk of violence
													at0032,    -- Moderate risk of violence
													at0033]    -- High risk of violence
												}
											}
										}
									}
								}
							}
						}
					}
					POINT_EVENT[at0088] occurrences matches {0..*} matches {    -- Evening shift
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
					}
					POINT_EVENT[at0113] occurrences matches {0..*} matches {    -- Night shift
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
					}
					EVENT[at0138] occurrences matches {0..*} matches {    -- Any event
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0016] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0163] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Brøset Violence Checklist (BVC) (en)">
					description = <"En klinisk skår som brukes i sykehus til å predikere nært forestående voldelig adferd hos psykiatriske pasienter over 18 år.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Dagvakt">
					description = <"Tidspunkt som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Forvirret">
					description = <"Pasienten opptrer åpenbart forvirret og desorientert. Kan være ute av stand til å gjøre rede for tid, sted og person.">
					comment = <"Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0005"] = <
					text = <"Irritabel">
					description = <"Blir lett sint og irritabel. Tåler dårlig andres nærvær.">
					comment = <"Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0006"] = <
					text = <"Brautende atferd">
					description = <"Atferden er overdrevent støyende.">
					comment = <"For eksempel roper i stedet for å snakke, slår med dørene o.l. Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0007"] = <
					text = <"Verbale trusler">
					description = <"Et verbalt utbrudd som er mer enn bare å heve røsten og har til hensikt å ydmyke eller skremme en annen person.">
					comment = <"For eksempel verbale utbrudd, nedlatende tale, nøytrale ytringer i et aggressivt toneleie. Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0008"] = <
					text = <"Fysiske trusler">
					description = <"Pasienten har en klart hensikt å fysisk true en annen person.">
					comment = <"For eksempel en aggressiv kroppsholdning, tar tak i klærne til en annen person, knytter neven o.l. Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0009"] = <
					text = <"Slag, spark mv mot gjenstander">
					description = <"Pasienten kommer oftere emd slag,spark etc. mot inventar enn normalt.">
					comment = <"For eksempel kaster ting, slår på eller knuser vindu, sparker i stykker møbler o.l. Kjenner du ikke pasienten og atferd er observert skårer man at atferden er tilstede.">
				>
				["at0016"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0017"] = <
					text = <"Total skår">
					description = <"Summen av poeng på hvert enkelt parameter i BCV skåren.">
				>
				["at0018"] = <
					text = <"Tilstede">
					description = <"">
				>
				["at0024"] = <
					text = <"Ikke tilstede">
					description = <"">
				>
				["at0030"] = <
					text = <"Tolkning">
					description = <"Tolkning av BCV totalskåren.">
				>
				["at0031"] = <
					text = <"Ingen eller liten risiko for vold">
					description = <"BVC skår 0: Det er ingen eller liten risiko for vold.">
				>
				["at0032"] = <
					text = <"Moderat risiko for vold">
					description = <"BVC skår 1-2: Det er en moderat risiko for vold. Forebyggende forholdsregler bør iverksettes.">
				>
				["at0033"] = <
					text = <"Høy risiko for vold">
					description = <"BVC skår 3-6: Høy risiko for vold, forebyggende forholdsregler skal iverksettes og planer for hvordan møte et eventuelt angrep legges.">
				>
				["at0088"] = <
					text = <"Kveldsvakt">
					description = <"Tidspunkt som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0113"] = <
					text = <"Nattevakt">
					description = <"Tidspunkt som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0138"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0163"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Brøset Violence Checklist (BVC)">
					description = <"An assessment score used within a hospital setting to assists in the prediction of imminent violence in psyciatric patients over the age of 18.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Day shift">
					description = <"Point in time event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Confused">
					description = <"Appears obviously confused and disorientated. May be unaware of time, place or person.">
					comment = <"If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0005"] = <
					text = <"Irritable">
					description = <"Easily annoyed or angered. Unable to tolerate the presence of others.">
					comment = <"If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0006"] = <
					text = <"Boisterous">
					description = <"Behaviour is overtly \"loud\" or noisy.">
					comment = <"For example slams doors, shouts out when talking etc. If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0007"] = <
					text = <"Verbal threats">
					description = <"A verbal outburst which is more than just a raised voice; and where there is a definite intent to intimidate or threaten another person.">
					comment = <"For example verbal attacks, abuse, name-calling, verbally neutral comments uttered in a snarling aggressive manner. If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0008"] = <
					text = <"Physical threats">
					description = <"Where there is a definite intent to physically threaten another person. ">
					comment = <"For example the taking of an aggressive stance; the grabbing of another persons clothing; the raising of an arm, leg, making of a fist or modelling of a head-butt directed at another. If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0009"] = <
					text = <"Atacking objects">
					description = <"An attack directed at an object and not an individual.">
					comment = <"For example the indiscriminate throwing of an object; banging or smashing windows; kicking, banging or head-butting an object; or the smashing of furniture. If behaviour is normal for a well known client, only an increase in behaviour scores 1.">
				>
				["at0016"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0017"] = <
					text = <"Total score">
					description = <"The total sum of each component variable for the BCV score.">
				>
				["at0018"] = <
					text = <"Present">
					description = <"">
				>
				["at0024"] = <
					text = <"Absent">
					description = <"">
				>
				["at0030"] = <
					text = <"Interpretation">
					description = <"Interpretation of the total score.">
				>
				["at0031"] = <
					text = <"Small risk of violence">
					description = <"BVC score 0: The risk of violence is small.">
				>
				["at0032"] = <
					text = <"Moderate risk of violence">
					description = <"BVC score 1-2: The risk of violence is moderate. Preventive measures should be taken.">
				>
				["at0033"] = <
					text = <"High risk of violence">
					description = <"BVC score 3-6: The risk of violence is very high. Preventive measures should be taken In addition, a plans should be developed to manage the potential violence.">
				>
				["at0088"] = <
					text = <"Evening shift">
					description = <"Point in time event which may be explicitly defined in a template or at run-time.">
				>
				["at0113"] = <
					text = <"Night shift">
					description = <"Point in time event which may be explicitly defined in a template or at run-time.">
				>
				["at0138"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0163"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
	>
