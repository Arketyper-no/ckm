archetype (adl_version=1.4; uid=d2328f1d-0006-4353-8fe3-e925df51556f)
	openEHR-EHR-EVALUATION.communication_capability.v0

concept
	[at0000]	-- Communication capability
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen HF, Norway">
				["email"] = <"john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-03-26">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om individets praktiske evne til å kommunisere, inkludert funksjonsnedsettelser og behov for kommunikasjonshjelpemidler.">
			use = <"Brukes for å registrere detaljer om individets praktiske evne til å kommunisere, inkludert funksjonsnedsettelser og behov for kommunikasjonshjelpemidler.

Hensikten med arketypen er å registrere hvordan individet kommuniserer med andre og hvordan klinikere best kan kommunisere med individet.

Arketypen CLUSTER.language (Språk) kan nøstes i SLOTene \"Foretrukket kommunikasjon\" og \"Annen kommunikasjon\".">
			keywords = <"språk", "tolk", "oversettelse", "oversetter", "kommunikasjon", "informasjonsutveksling", "formidle", "videreformidle", "tilpasset kommunikasjon", "tilrettelagt kommunikasjon">
			misuse = <"Brukes ikke for å registrere detaljer om et språk, bruk arketypen CLUSTER.language (Språk) for dette formålet.

Brukes ikke for å registrere detaljer om en forespørsel om tolketjenester. Bruk arketypen CLUSTER.interpreter_requirements (Tolkebehov) i en egnet INSTRUCTION-arketype, for eksempel INSTRUCTION.service_request (Helsetjenesteforespørsel).

Brukes ikke for å registrere detaljer om tolking som er gjennomført. Bruk arketypen ACTION.interpretation for dette formålet.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record detail about the practical ability of the individual to communicate, including impairments and need for communication aids.">
			use = <"Use to record detail about the practical ability of the individual to communicate, including impairments and need for communication aids.

This archetype is intended to capture how the individual communicates with others and how healthcare providers can best communicate with the individual.

This archetype is designed to contain the CLUSTER.language archetype in one or both SLOTS for 'Preferred communication' and 'Other communication'.">
			keywords = <"language", "interpreter", "translation", "translater", "translator">
			misuse = <"Not to be used to record details about a language - use CLUSTER.language for this purpose.

Not to be used to record details about a request for interpreter services - use CLUSTER.Interpreter_requirements with a suitable INSTRUCTION archetype, for example INSTRUCTION.service_request.

Not to be used to record details about an interpretation that was performed - use the proposed ACTION.interpretation.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Rose Mari Eikås, Helse Bergen, Norway", "Valborg Ellingsen, Haraldsplass Diakonale sykehus, Norway", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway", "Jessica Hansen, Universitetet i Oslo, Norway", "Erling Are Hole, Helse Bergen, Norway", "Tom Jarl Jakobsen, Helse Bergen, Norway", "Øygunn Leite Kallevik, Helse Bergen, Norway", "Lars Morgan Karlsen, Nordlandssykehuset Bodø, Norway", "Siren Kraakenes, Helse Vest IKT, Norway", "Siv Marie Lien, DIPS ASA, Norway", "Lars Ivar Mehlum, Helse Bergen HF, Norway", "Anine Ramberg, DIPS ASA, Norway", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Laila Storesund, Haraldsplass diakonale sykehus, Norway", "Susanne Trønnes, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Nasjonal IKT">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"no.nasjonalikt">
		["original_publisher"] = <"Nasjonal IKT">
		["custodian_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"42147262B4D639F667C39DBA78A77780">
		["build_uid"] = <"010faa88-2ff5-482b-8c3c-d6dcc4c81652">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {	-- Communication capability
		data matches {
			ITEM_TREE[at0001] matches {	-- ItemTree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {	-- Preferred communication
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
					}
					allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Other communication
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
					}
					allow_archetype CLUSTER[at0005] occurrences matches {0..1} matches {	-- Additional details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device\.v1/}
					}
					CLUSTER[at0006] occurrences matches {0..*} matches {	-- Per language
						items cardinality matches {2..*; unordered} matches {
							allow_archetype CLUSTER[at0007] occurrences matches {0..1} matches {	-- Language details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.language\.v0/}
							}
							ELEMENT[at0008] matches {	-- Translator required
								value matches {
									DV_BOOLEAN matches {
										value matches {True, False}
									}
								}
							}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0011] matches {	-- ItemTree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0012] occurrences matches {0..1} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Communication capability">
					description = <"The practical ability of the individual to communicate.">
				>
				["at0001"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Description">
					description = <"Narrative description about the overall capability of the individual to communicate, including impairments and need for communication aids.">
				>
				["at0003"] = <
					text = <"Preferred communication">
					description = <"The preferred language and method of communication for an individual.">
					comment = <"More than one instances of the CLUSTER.language may be required here if the individual uses a variety modes of communication, for example speaking in one language and signing in another.">
				>
				["at0004"] = <
					text = <"Other communication">
					description = <"Other languages, and/or methods of communication for an individual.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about the individual's communication capability.">
					comment = <"For example: a hearing aid or tactile reading device.">
				>
				["at0006"] = <
					text = <"Per language">
					description = <"Details about the capabilities of the individual for a specific language.">
					comment = <"In clinical systems it is common to record whether a interpreter is required as a simple, stand-alone boolean data point - the language for translation required is implied, rather than specified. However if this single boolean is exchanged to a location where the assumed language is different, the recorded boolean may be incorrect. In order for this data to be correctly exchanged, both the language and requirement for translation needs to be exchanged as a pair. For each language that is proposed for a clinical communication it will be necessary to do a separate assessment about whether a translator is required for the individual. For example, an English speaker in Quebec will not require an English interpreter but may require one if the consultation is being conducted in French. ">
				>
				["at0007"] = <
					text = <"Language details">
					description = <"Details about a language.">
				>
				["at0008"] = <
					text = <"Translator required">
					description = <"Indicator that the services of a translator are required for the identified language.">
				>
				["at0009"] = <
					text = <"Comment">
					description = <"Additional narrative about the communication capability of an individual not captured in other fields.">
				>
				["at0011"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
				>
				["at0013"] = <
					text = <"Last updated">
					description = <"Date when the Communication capability was last updated.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Evne til kommunikasjon">
					description = <"Individets praktiske evne til å kommunisere.">
				>
				["at0001"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Beskrivelse">
					description = <"En fritekstbeskrivelse om individets overordnede evne til kommunikasjon, inkludert funksjonsnedsettelser og behov for kommunikasjonshjelpemidler.">
				>
				["at0003"] = <
					text = <"Foretrukket kommunikasjon">
					description = <"Individets foretrukne språk og kommunikasjonsmetode.">
					comment = <"Det kan være behov for flere instanser av CLUSTER.language (Språk) om individet benytter flere kommunikasjonsmetoder. For eksempel: snakker ett språk og benytter et annet for tegnspråk.">
				>
				["at0004"] = <
					text = <"Annen kommunikasjon">
					description = <"Individets andre språk og kommunikasjonsmetoder.">
				>
				["at0005"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om individets evne til å kommunisere.">
					comment = <"For eksempel detaljer om høreapparat eller leselist.">
				>
				["at0006"] = <
					text = <"Per språk">
					description = <"Detaljer om individets evne til kommunikasjon for et spesifikt språk.">
					comment = <"I kliniske system er det vanlig å registrere om det er behov for tolk med en registrering i et enkelt boolsk dataelement. Oversettelsesspråket er underforstått og spesifiseres ikke. Utveksles denne verdien til en lokalisering hvor oversettelsesspråket er et annet kan den registrerte boolske verdien være feil. For å sikre en riktig utveksling av dette dataelementet, må både språket og behov for tolk utveksles sammen. Hvert språk som individet snakker vil trenge en en egen vurdering av om det er et tolkebehov. For eksempel vil ikke en engelsktalende i Quebec trenge engelsk tolk, men det kan være behov for tolk om konsultasjonen utføres på fransk.">
				>
				["at0007"] = <
					text = <"Språkdetaljer">
					description = <"Detaljer om et språk.">
				>
				["at0008"] = <
					text = <"Individet trenger tolk">
					description = <"Indikererer om det er et tolkebehov på det spesifiserte språket.">
				>
				["at0009"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse om individets evne til kommunikasjon som ikke er registrert i andre felt.">
				>
				["at0011"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
				>
				["at0013"] = <
					text = <"Sist oppdatert">
					description = <"Datoen da evne til kommunikasjon sist ble oppdatert.">
				>
			>
		>
	>
